/*
PUS SERVICE 20 : PARAMETER MANAGEMENT
*/

import service_libraries.pus_services.pus_service20.pus_service20_help;

interface PUSS20Iface {
    procedure exec_tc (&mut self, tc_handler : &mut TCHandlerT, action_status: &mut Status<i32>);
};

resource class PUSService20 provides PUSS20Iface {

    exec_tc_req_status : PSExecTCReqStatus;
    exec_tc_req_status_update : PS20ExecTCReqStatusUpdate;

    system_data_pool_u32 : access AtomicArrayAccess<u32; sdp_num_u32_params>;
    system_data_pool_u8 : access AtomicArrayAccess<u8; sdp_num_u8_params>;

    tm_channel: access TMChannelIface;   
    a_tm_handler_pool : access Allocator<TMHandlerT>;
    tm_counter : access TMCounterIface;

    pus_service_9 : access PUSS9Iface;


    viewer build_tm_20_2(&self, p_tm_handler : &mut TMHandlerT, tm_seq_counter : u16, result : &mut MyResult){

        startup_tm(p_tm_handler);

        append_u8_appdata_field(p_tm_handler, 1, result);
        append_u16_appdata_field(p_tm_handler, (self->exec_tc_req_status_update.PID) as u16, result);

        var item_type : DataPoolItemType = sys_data_pool_get_item_type((self->exec_tc_req_status_update.PID));

        match item_type {
            case u8_t => {
                var u8_SDP_value : u8 = 0;
                self->system_data_pool_u8.load_index(((self->exec_tc_req_status_update.PID)) as usize, &mut u8_SDP_value);
                append_u8_appdata_field(p_tm_handler, u8_SDP_value, result);
            }
            case u32_t => {
                var u32_SDP_value : u32 = 0;
                self->system_data_pool_u32.load_index(((self->exec_tc_req_status_update.PID)) as usize, &mut u32_SDP_value);
                append_u32_appdata_field(p_tm_handler, u32_SDP_value, result);
            }
            case NoValidPID => {
                *result = MyResult::Error;
            }
        }

        var current_obt : MissionObt = {seconds = 0, finetime = 0};
        self->pus_service_9.get_current_obt(&mut current_obt);

        close_tm(p_tm_handler, 20, 2, tm_seq_counter, current_obt);

        return;

    }


    method exec20_1TC(&priv self) -> PSExecTCReqStatus {

        var next_status : PSExecTCReqStatus = PSExecTCReqStatus::Exit;
        var result : MyResult = MyResult::Ok; 
        var ack_enabled : bool = false;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                if (self->exec_tc_req_status_update.N != 1) {

                    self->pus_service_9.get_current_obt(&mut current_obt);
                    build_tm_1_4_num_of_instr_not_valid(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                 self->exec_tc_req_status_update.N, current_obt, &mut result);

                    if (result is MyResult::Ok){

                        self->tm_channel.send_tm(b_tm_handler, &mut result);

                        if (result is MyResult::Error){
                            next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                        }
                    } 
                    else {
                        self->a_tm_handler_pool.free(b_tm_handler);
                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                    }
                    
                }
                else {

                    if (sys_data_pool_is_valid_PID(self->exec_tc_req_status_update.PID)){

                        self->pus_service_9.get_current_obt(&mut current_obt);
                        build_tm_1_3(&mut b_tm_handler, tm_count, self->exec_tc_req_status_update.flags_ack, current_obt, &mut result, &mut ack_enabled);

                        if (ack_enabled) {

                            if (result is MyResult::Ok){

                                self->tm_channel.send_tm(b_tm_handler, &mut result);

                                if (result is MyResult::Error){
                                    next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                }
                            }
                            else {
                                self->a_tm_handler_pool.free(b_tm_handler);
                                next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                            }

                        } else {
                            self->a_tm_handler_pool.free(b_tm_handler);
                        }

                        var tm_handler2 : Option<box TMHandlerT> = None;
                        self->a_tm_handler_pool.alloc(&mut tm_handler2);

                        match tm_handler2 {

                            case Some(b_tm_handler2) => {

                                var tm_count2 : u16 = 0 : u16;
                                self->tm_counter.get_next_tm_count(&mut tm_count2);
                                self->build_tm_20_2(&mut b_tm_handler2, tm_count2, &mut result);

                                if (result is MyResult::Ok){

                                    self->tm_channel.send_tm(b_tm_handler2, &mut result);

                                    if (result is MyResult::Error){
                                        
                                        
                                    }
                                } 
                                else {
                                    self->a_tm_handler_pool.free(b_tm_handler2);
                                    next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                                }
                            
                            }
                            case None => {

                                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
                                
                            }
                        }

                        var tm_handler3 : Option<box TMHandlerT> = None;
                        self->a_tm_handler_pool.alloc(&mut tm_handler3);

                        match tm_handler3 {

                            case Some(b_tm_handler3) => {

                                var tm_count3 : u16 = 0 : u16;
                                self->tm_counter.get_next_tm_count(&mut tm_count3);
                                self->pus_service_9.get_current_obt(&mut current_obt);
                                build_tm_1_7(&mut b_tm_handler3, tm_count3, self->exec_tc_req_status_update.flags_ack, current_obt, &mut result, &mut ack_enabled);

                                if (ack_enabled) {

                                    if (result is MyResult::Ok){

                                        self->tm_channel.send_tm(b_tm_handler3, &mut result);

                                        if (result is MyResult::Error){
                                            next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                        }
                                    }
                                    else {
                                        self->a_tm_handler_pool.free(b_tm_handler3);
                                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                                    }

                                } else {
                                    self->a_tm_handler_pool.free(b_tm_handler3);
                                }
                                
                            }
                            case None => {
                                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
                            }
                        }

                            
                    } else {

                        self->pus_service_9.get_current_obt(&mut current_obt);
                        build_tm_1_4_PID_not_valid(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                     self->exec_tc_req_status_update.PID, current_obt, &mut result);

                        if (result is MyResult::Ok){

                            self->tm_channel.send_tm(b_tm_handler, &mut result);

                            if (result is MyResult::Error){
                                next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                            }
                        } 
                        else {
                            self->a_tm_handler_pool.free(b_tm_handler);
                            next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                        }
                          
                    }
                }
                
            }
            case None => {
                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
            }
        }


        return next_status;
    }



    method exec20_3TC(&priv self) -> PSExecTCReqStatus {

        var next_status : PSExecTCReqStatus = PSExecTCReqStatus::Exit;
        var result : MyResult = MyResult::Ok;
        var ack_enabled : bool = false;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                if (self->exec_tc_req_status_update.N != 1) {

                    self->pus_service_9.get_current_obt(&mut current_obt);
                    build_tm_1_4_num_of_instr_not_valid(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                             self->exec_tc_req_status_update.N, current_obt, &mut result);

                    if (result is MyResult::Ok){

                        self->tm_channel.send_tm(b_tm_handler, &mut result);

                        if (result is MyResult::Error){
                            next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                        }
                    } 
                    else {
                        self->a_tm_handler_pool.free(b_tm_handler);
                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                    }
                    
                }
                else {

                    if (sys_data_pool_is_valid_PID(self->exec_tc_req_status_update.PID)){

                        if(sys_data_pool_is_PID_update_via_TC_enabled(self->exec_tc_req_status_update.PID)){

                            self->pus_service_9.get_current_obt(&mut current_obt);
                            build_tm_1_3(&mut b_tm_handler, tm_count, self->exec_tc_req_status_update.flags_ack, current_obt, &mut result, &mut ack_enabled);

                            if (ack_enabled) {

                                if (result is MyResult::Ok){

                                    self->tm_channel.send_tm(b_tm_handler, &mut result);

                                    if (result is MyResult::Error){
                                        next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                    }
                                }
                                else {
                                    self->a_tm_handler_pool.free(b_tm_handler);
                                    next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                                }

                            } else {
                                self->a_tm_handler_pool.free(b_tm_handler);
                            }
                            

                            var item_type : DataPoolItemType = sys_data_pool_get_item_type(self->exec_tc_req_status_update.PID);

                            var succesful_execution : bool = false;

                            match item_type {
                                case u8_t => {
                                    
                                    self->system_data_pool_u8.store_index(self->exec_tc_req_status_update.PID as usize, self->exec_tc_req_status_update.tc_20_3_data.PID_value_u8);
                                    succesful_execution = true;
                                }
                                case u32_t => {

                                    self->system_data_pool_u32.store_index(self->exec_tc_req_status_update.PID as usize, self->exec_tc_req_status_update.tc_20_3_data.PID_value_u32);
                                    succesful_execution = true;
                                }
                                case NoValidPID => {
                                    result = MyResult::Error;
                                }
                            }

                            if (succesful_execution){

                                var tm_handler2 : Option<box TMHandlerT> = None;
                                self->a_tm_handler_pool.alloc(&mut tm_handler2);

                                match tm_handler2 {

                                    case Some(b_tm_handler2) => {

                                        var tm_count2 : u16 = 0 : u16;
                                        self->tm_counter.get_next_tm_count(&mut tm_count2);
                                        self->pus_service_9.get_current_obt(&mut current_obt);
                                        build_tm_1_7(&mut b_tm_handler2, tm_count2, self->exec_tc_req_status_update.flags_ack, current_obt, &mut result, &mut ack_enabled);

                                        if (ack_enabled) {

                                            if (result is MyResult::Ok){

                                                self->tm_channel.send_tm(b_tm_handler2, &mut result);

                                                if (result is MyResult::Error){
                                                    next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                                }
                                            }
                                            else {
                                                self->a_tm_handler_pool.free(b_tm_handler2);
                                                next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                                            }

                                        } else {
                                            self->a_tm_handler_pool.free(b_tm_handler2);
                                        }
                                        
                                    }
                                    case None => {
                                        next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
                                    }
                                }
                            }


                        }
                        else {

                            self->pus_service_9.get_current_obt(&mut current_obt);
                            build_tm_1_4_PID_read_only_via_TC (&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                             self->exec_tc_req_status_update.PID, current_obt, &mut result);

                            if (result is MyResult::Ok){

                                self->tm_channel.send_tm(b_tm_handler, &mut result);

                                if (result is MyResult::Error){
                                    next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                }
                            } 
                            else {
                                self->a_tm_handler_pool.free(b_tm_handler);
                                next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                            }
                            
                        }
                    }
                    else {

                        self->pus_service_9.get_current_obt(&mut current_obt);
                        build_tm_1_4_PID_not_valid(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                     self->exec_tc_req_status_update.PID, current_obt, &mut result);

                        if (result is MyResult::Ok){

                            self->tm_channel.send_tm(b_tm_handler, &mut result);

                            if (result is MyResult::Error){
                                next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                            }
                        } 
                        else {
                            self->a_tm_handler_pool.free(b_tm_handler);
                            next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                        }
                    }
                }
            
            }
            case None => {
                
                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        return next_status;

    }

    viewer get_TC_params(&self, tc_handler : &mut TCHandlerT, subtype : &mut u8, result : &mut MyResult) -> PS20ExecTCReqStatusUpdate {

        *subtype = tc_handler->df_header.subtype;

        var tc_data : PS20ExecTCReqStatusUpdate = {
            packet_id = 0,
            packet_error_ctrl = 0,
            flags_ack = 0,
            tc_num_bytes = 0,
            N = 0,
            PID = 0,
            tc_20_3_data = {
                PID_value_u8 = 0,
                PID_value_u32 = 0
            }
        };
        tc_data.packet_id = tc_handler->packet_header.packet_id;
        tc_data.packet_error_ctrl = tc_handler->packet_error_ctrl;
        tc_data.flags_ack = tc_handler->df_header.flag_ver_ack;
        tc_data.tc_num_bytes = tc_handler->tc_descriptor.tc_num_bytes;
        *result = tc_handler_get_u8_appdata_field(tc_handler, &mut (tc_data.N));
        *result = tc_handler_get_u16_appdata_field(tc_handler, &mut (tc_data.PID));

        if (*subtype == 3){

            var item_type : DataPoolItemType = sys_data_pool_get_item_type(tc_data.PID);

            match item_type {
                case u8_t => {
                    *result = tc_handler_get_u8_appdata_field(tc_handler, &mut (tc_data.tc_20_3_data.PID_value_u8));
                }
                case u32_t => {
                    *result = tc_handler_get_u32_appdata_field(tc_handler, &mut (tc_data.tc_20_3_data.PID_value_u32));
                }
                case NoValidPID => {
                    *result = MyResult::Error;
                }
            }
        }

        return tc_data;

    }

    viewer manage_short_pack_length_error(&self) -> PSExecTCReqStatus {

        var next_status : PSExecTCReqStatus = PSExecTCReqStatus::Exit;
        var result : MyResult = MyResult::Ok;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                self->pus_service_9.get_current_obt(&mut current_obt);
                build_tm_1_4_short_pack_length(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                 (self->exec_tc_req_status_update.tc_num_bytes), current_obt, &mut result);
                
                if (result is MyResult::Ok){

                    self->tm_channel.send_tm(b_tm_handler, &mut result);

                    if (result is MyResult::Error){
                        next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                    }
                } 
                else {
                    self->a_tm_handler_pool.free(b_tm_handler);
                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                }
                
            }
            case None => {
                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        return next_status;
    }

    viewer manage_error_in_acceptance(&self) -> PSExecTCReqStatus {

        var next_status : PSExecTCReqStatus = PSExecTCReqStatus::Exit;
        var result : MyResult = MyResult::Ok;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                self->pus_service_9.get_current_obt(&mut current_obt);
                build_tm_1_4_error_in_acceptance(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                 current_obt, &mut result);
                
                if (result is MyResult::Ok){

                    self->tm_channel.send_tm(b_tm_handler, &mut result);

                    if (result is MyResult::Error){
                        next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                    }
                } 
                else {
                    self->a_tm_handler_pool.free(b_tm_handler);
                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                }
                
            }
            case None => {
                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        return next_status;
    }

    viewer manage_tm_limit_app_data_reached(&self) -> PSExecTCReqStatus {

        var next_status : PSExecTCReqStatus = PSExecTCReqStatus::Exit;
        var result : MyResult = MyResult::Ok;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                self->pus_service_9.get_current_obt(&mut current_obt);
                build_tm_1_8_tm_exceed_limit_appdata(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                 current_obt, &mut result);
                
                if (result is MyResult::Ok){

                    self->tm_channel.send_tm(b_tm_handler, &mut result);

                    if (result is MyResult::Error){
                        next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                    }
                } 
                else {
                    self->a_tm_handler_pool.free(b_tm_handler);
                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                }
                
            }
            case None => {
                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        return next_status;
    }


    procedure exec_tc (&mut self, tc_handler : &mut TCHandlerT, action_status: &mut Status<i32>) {

        var subtype : u8 = 0;

        for i : usize in 0 .. 3 while (self->exec_tc_req_status is PSExecTCReqStatus::Exit == false) {

            match self->exec_tc_req_status {

                case Init => {

                    var result : MyResult = MyResult::Ok;

                    self->exec_tc_req_status_update = self->get_TC_params(tc_handler, &mut subtype, &mut result);

                    if (result is MyResult::Error) {
                        self->exec_tc_req_status = PSExecTCReqStatus::Error(TC_DATA_OUT_OF_RANGE_ERROR);
                    } else {
                        self->exec_tc_req_status = PSExecTCReqStatus::ExecTC;
                    }

                }
                case ExecTC => {

                    if (subtype == 1) {

                        self->exec_tc_req_status = self->exec20_1TC();

                    } else if (subtype == 3) {

                        self->exec_tc_req_status = self->exec20_3TC();

                    } else {

                        self->exec_tc_req_status = PSExecTCReqStatus::Error(ACCEPTANCE_ERROR);
                    }
                }

                case Error(error_code) => {

                    self->exec_tc_req_status = PSExecTCReqStatus::Exit;

                    if (error_code == ACCEPTANCE_ERROR) {

                        //Generar TM[1,4] error en la aceptación
                        self->exec_tc_req_status = self->manage_error_in_acceptance();
                        
                    } else if (error_code == BUILD_TM_ERROR) {

                        //Generar TM[1,8] error en la generación de TM
                        self->exec_tc_req_status = self->manage_tm_limit_app_data_reached();
                       
                    } else if (error_code == TC_DATA_OUT_OF_RANGE_ERROR) {

                        self->exec_tc_req_status = self->manage_short_pack_length_error();
                      
                    } else {
                        
                       
                    }

                }
                case Failure(failure_code) => {

                    *action_status = Failure(failure_code);
                    self->exec_tc_req_status = PSExecTCReqStatus::Exit;

                }
                case Exit => {
                    //Unreachable point
                }  
            }
        }

        if(self->exec_tc_req_status is PSExecTCReqStatus::Exit){

            self->exec_tc_req_status = PSExecTCReqStatus::Init;
        }

        return;
    
    }

};