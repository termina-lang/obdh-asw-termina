/*
PUS SERVICE 5 : EVENT REPORTING
*/


import service_libraries.pus_services.pus_service5.pus_service5_help;



interface PUSS5Iface {

    procedure exec_tc (&mut self, tc_handler : &mut TCHandlerT, action_status: &mut Status<i32>);
    procedure is_Ev_ID_enabled_ext(&mut self, Ev_ID: u16, p_enabled: &mut bool); 
    procedure build_and_tx_tm_5_x(&mut self, status: &mut Status<i32>, evID: u16, fault_info: FaultInfo);
    procedure build_and_tx_tm_5_2(&mut self, status: &mut Status<i32>);

};

resource class PUSService5 provides PUSS5Iface {

    Ev_ID_enable_config : [u32; 4];

    exec_tc_req_status : PSExecTCReqStatus;
    exec_tc_req_status_update : PS5ExecTCReqStatusUpdate;

    tm_channel : access TMChannelIface;
    a_tm_handler_pool : access Allocator<TMHandlerT>;
    tm_counter : access TMCounterIface;

    pus_service_9 : access PUSS9Iface;
    

    viewer is_Ev_ID_enabled(&self, Ev_ID : u16) -> bool {

        var enabled : bool = false;
        var index : usize = get_Ev_ID_enable_config_index(Ev_ID);
        var offset : u8 = get_Ev_ID_enable_config_offset(Ev_ID);

        if (index < 4){

            if(((self->Ev_ID_enable_config[index] >> offset) & 0x01) != 0){
                enabled = true;
            }
        }

        return enabled;
    }

    procedure is_Ev_ID_enabled_ext(&mut self, Ev_ID: u16, p_enabled: &mut bool) {

        *p_enabled = (self->is_Ev_ID_enabled(Ev_ID));

        return;
    }


    method exec5_5TC(&priv self) -> Status<i32> {

        var ack_enabled : bool = false;
        var status : Status<i32> = Success;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                var index: usize = get_Ev_ID_enable_config_index(self->exec_tc_req_status_update.EvID);
                var offset : u8 = get_Ev_ID_enable_config_offset(self->exec_tc_req_status_update.EvID);

            
                if (self->exec_tc_req_status_update.N != 1) {

                    self->pus_service_9.get_current_obt(&mut current_obt);
                    build_tm_1_4_num_of_instr_not_valid(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                 self->exec_tc_req_status_update.N, current_obt, &mut status);

                    if (status is Success){

                        self->tm_channel.send_tm(b_tm_handler, &mut status);

                    } 
                    else {
                        self->a_tm_handler_pool.free(b_tm_handler);
                        
                    }
                }
                else {

                    var is_valid_EvID : bool = index < 4;

                    if(is_valid_EvID) {

                        self->pus_service_9.get_current_obt(&mut current_obt);
                        build_tm_1_3(&mut b_tm_handler, tm_count, self->exec_tc_req_status_update.flags_ack, current_obt, &mut status, &mut ack_enabled);

                        if (ack_enabled) {

                            if (status is Success){

                                self->tm_channel.send_tm(b_tm_handler, &mut status);

                            }
                            else {
                                self->a_tm_handler_pool.free(b_tm_handler);
                            }

                        } else {
                            self->a_tm_handler_pool.free(b_tm_handler);
                        }

                        self->Ev_ID_enable_config[index] = self->Ev_ID_enable_config[index] | (1 << offset);

                        var tm_handler2 : Option<box TMHandlerT> = None;
                        self->a_tm_handler_pool.alloc(&mut tm_handler2);

                        match tm_handler2 {

                            case Some(b_tm_handler2) => {

                                var tm_count2 : u16 = 0 : u16;
                                self->tm_counter.get_next_tm_count(&mut tm_count2);
                                self->pus_service_9.get_current_obt(&mut current_obt);
                                build_tm_1_7(&mut b_tm_handler2, tm_count2, self->exec_tc_req_status_update.flags_ack, current_obt, &mut status, &mut ack_enabled);

                                if (ack_enabled) {

                                    if (status is Success){

                                        self->tm_channel.send_tm(b_tm_handler2, &mut status);

                                    }
                                    else {
                                        self->a_tm_handler_pool.free(b_tm_handler2);
                                    }

                                } else {
                                    
                                    self->a_tm_handler_pool.free(b_tm_handler2);
                                }
                        
                            }
                            case None => {
                                status = Failure(TM_POOL_ALLOC_FAILURE);
                            }
                        }

                    } else {

                        self->pus_service_9.get_current_obt(&mut current_obt);
                        build_tm_1_4_EvID_not_valid(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                 (self->exec_tc_req_status_update.EvID), current_obt, &mut status);

                        if (status is Success){

                            self->tm_channel.send_tm(b_tm_handler, &mut status);

                        } 
                        else {
                            self->a_tm_handler_pool.free(b_tm_handler);
                            
                        }

                    }

                }
            }
            case None => {
                status = Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        return status;

    }

    method exec5_6TC(&priv self) -> Status<i32> {

        var ack_enabled : bool = false;
        var status : Status<i32> = Success;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                var index: usize = get_Ev_ID_enable_config_index(self->exec_tc_req_status_update.EvID);
                var offset : u8 = get_Ev_ID_enable_config_offset(self->exec_tc_req_status_update.EvID);

            
                if (self->exec_tc_req_status_update.N != 1) {

                    self->pus_service_9.get_current_obt(&mut current_obt);
                    build_tm_1_4_num_of_instr_not_valid(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                 self->exec_tc_req_status_update.N, current_obt, &mut status);

                    if (status is Success){

                        self->tm_channel.send_tm(b_tm_handler, &mut status);

                    } 
                    else {
                        self->a_tm_handler_pool.free(b_tm_handler);

                    }
                   
                }
                else {

                    var is_valid_EvID : bool = index < 4;

                    if(is_valid_EvID) {

                        self->pus_service_9.get_current_obt(&mut current_obt);
                        build_tm_1_3(&mut b_tm_handler, tm_count, self->exec_tc_req_status_update.flags_ack, current_obt, &mut status, &mut ack_enabled);

                        if (ack_enabled) {

                            if (status is Success){

                                self->tm_channel.send_tm(b_tm_handler, &mut status);

                            }
                            else {
                                self->a_tm_handler_pool.free(b_tm_handler);
                            }

                        } else {
                            self->a_tm_handler_pool.free(b_tm_handler);
                        }

                        self->Ev_ID_enable_config[index] = self->Ev_ID_enable_config[index] & (0xFFFFFFFE << offset);

                        var tm_handler2 : Option<box TMHandlerT> = None;
                        self->a_tm_handler_pool.alloc(&mut tm_handler2);

                        match tm_handler2 {

                            case Some(b_tm_handler2) => {

                                var tm_count2 : u16 = 0 : u16;
                                self->tm_counter.get_next_tm_count(&mut tm_count2);
                                self->pus_service_9.get_current_obt(&mut current_obt);
                                build_tm_1_7(&mut b_tm_handler2, tm_count2, self->exec_tc_req_status_update.flags_ack, current_obt, &mut status, &mut ack_enabled);

                                if (ack_enabled) {

                                    if (status is Success){

                                        self->tm_channel.send_tm(b_tm_handler2, &mut status);

                                    }
                                    else {
                                        self->a_tm_handler_pool.free(b_tm_handler2);
                                    }

                                } else {
                                    self->a_tm_handler_pool.free(b_tm_handler2);
                                }
                            }
                            case None => {
                                status = Failure(TM_POOL_ALLOC_FAILURE);
                            }
                        }

                    } else {

                        self->pus_service_9.get_current_obt(&mut current_obt);
                        build_tm_1_4_EvID_not_valid(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl), 
                                (self->exec_tc_req_status_update.EvID), current_obt, &mut status);

                        if (status is Success){

                            self->tm_channel.send_tm(b_tm_handler, &mut status);

                        } 
                        else {
                            self->a_tm_handler_pool.free(b_tm_handler);
                            
                        }
                    }

                }
            }
            case None => {
                status = Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        return status;

    }





    procedure build_and_tx_tm_5_2(&mut self, status: &mut Status<i32>){ 

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                startup_tm(&mut b_tm_handler);
                append_u16_appdata_field(&mut b_tm_handler, EvID_build_tm_error, status);

                if(*status is Success){ 

                    var tm_count : u16 = 0 : u16;
                    self->tm_counter.get_next_tm_count(&mut tm_count);
                    var current_obt : MissionObt = {seconds = 0, finetime = 0};
                    self->pus_service_9.get_current_obt(&mut current_obt);
                    close_tm(&mut b_tm_handler, 5, 2, tm_count, current_obt);
                    self->tm_channel.send_tm(b_tm_handler, status);


                }
                else {
                    self->a_tm_handler_pool.free(b_tm_handler);
                }
            }
            case None => {
                *status = Failure(TM_POOL_ALLOC_FAILURE);
                
            }
        }
        return;

    }

    procedure build_and_tx_tm_5_x(&mut self, status: &mut Status<i32>, evID: u16, fault_info: FaultInfo){

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);
                var current_obt : MissionObt = {seconds = 0, finetime = 0};
                self->pus_service_9.get_current_obt(&mut current_obt);
                
                match (fault_info) {
                    case ParamOutOfLimit(fault_value_info) => {
                        build_tm_5_x_param_out_of_limit(&mut b_tm_handler, tm_count, fault_value_info, evID, current_obt, status);
                    }
                    case ParamFaultValue(fault_value_info) => {
                        build_tm_5_x_param_check_value_fail(&mut b_tm_handler, tm_count, fault_value_info, evID, current_obt, status);
                    }  
                    case Empty => {
                       
                    } 
                }

                if(*status is Success){

                    self->tm_channel.send_tm(b_tm_handler, status);

                } else {
                    //Generar TM[5,2]
                    self->a_tm_handler_pool.free(b_tm_handler);
                }
                

            }
            case None => {
                *status = Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        return;
    }


   

    procedure exec_tc (&mut self, tc_handler : &mut TCHandlerT, action_status: &mut Status<i32>){

        var status : Status<i32> = Success;
        var subtype : u8 = tc_handler->df_header.subtype;

        for i : usize in 0 .. 3 while (self->exec_tc_req_status is PSExecTCReqStatus::Exit == false) {

            match self->exec_tc_req_status {

                case Init => {

                    self->exec_tc_req_status_update.packet_id = tc_handler->packet_header.packet_id;
                    self->exec_tc_req_status_update.packet_error_ctrl = tc_handler->packet_error_ctrl;
                    self->exec_tc_req_status_update.flags_ack = tc_handler->df_header.flag_ver_ack;
                    self->exec_tc_req_status_update.tc_num_bytes = tc_handler->tc_descriptor.tc_num_bytes;
                    status = tc_handler_get_u8_appdata_field(tc_handler, &mut (self->exec_tc_req_status_update.N));
                    status = tc_handler_get_u16_appdata_field(tc_handler, &mut (self->exec_tc_req_status_update.EvID));

                    if (status is Success) {

                        self->exec_tc_req_status = PSExecTCReqStatus::ExecTC;
                        
                    } else {

                        status = Failure(ACCEPTANCE_ERROR);
                        self->exec_tc_req_status = PSExecTCReqStatus::Exit;
                        
                    }

                }
                case ExecTC => {

                    self->exec_tc_req_status = PSExecTCReqStatus::Exit;

                    if (subtype == 5) {

                        status = self->exec5_5TC();

                    } else if (subtype == 6) {

                        status = self->exec5_6TC();

                    } else {

                        status = Failure(ACCEPTANCE_ERROR);
                    }
                }
                case Exit => {
                    //Unreachable point
                }  
            }
        }

        match status {
                        
            case Success => {

                *action_status = Success;
    
            }
            case Failure(error_code) => {

                if (error_code == ACCEPTANCE_ERROR || error_code == BUILD_TM_ERROR || error_code == TC_DATA_OUT_OF_RANGE_ERROR) {

                    *action_status = Success;

                    var tm_handler : Option<box TMHandlerT> = None;
                    self->a_tm_handler_pool.alloc(&mut tm_handler);

                    match tm_handler {

                        case Some(b_tm_handler) => {

                            var tm_count : u16 = 0 : u16;
                            var current_obt : MissionObt = {seconds = 0, finetime = 0};
                            
                            self->tm_counter.get_next_tm_count(&mut tm_count);
                            self->pus_service_9.get_current_obt(&mut current_obt);

                            if (error_code == ACCEPTANCE_ERROR) {

                                build_tm_1_4_error_in_acceptance(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                 current_obt, &mut status);

                            } else if (error_code == BUILD_TM_ERROR) {

                                build_tm_1_8_tm_exceed_limit_appdata(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                            current_obt, &mut status);

                            } else if (error_code == TC_DATA_OUT_OF_RANGE_ERROR) {

                                build_tm_1_4_short_pack_length(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                    (self->exec_tc_req_status_update.tc_num_bytes), current_obt, &mut status);

                            } else {
                              

                            }

                            
                            
                            if (status is Success){

                                self->tm_channel.send_tm(b_tm_handler, &mut status);

                            } 
                            else {
                                self->a_tm_handler_pool.free(b_tm_handler);
                                
                            }
                            
                        }
                        case None => {
                            status = Failure(TM_POOL_ALLOC_FAILURE);
                        }
                    }
                    
                } else {

                    *action_status = Failure(error_code);
                    
                }   
                
            }  
        }

        self->exec_tc_req_status = PSExecTCReqStatus::Init;

        return;
    
    }
  
};



