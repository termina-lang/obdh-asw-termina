/*
PUS SERVICE 4 : PARAMETER STATISTICS REPORTING
*/

import service_libraries.pus_services.pus_service4.pus_service4_help;

interface PUSS4Iface {

    procedure exec_tc (&mut self, tc_handler : &mut TCHandlerT, action_status: &mut Status<i32>);
    procedure update_all_stats(&mut self);
    procedure startup(&mut self);
    
};

resource class PUSService4 provides PUSS4Iface {

    exec_tc_req_status : PSExecTCReqStatus;
    exec_tc_req_status_update : PS4ExecTCReqStatusUpdate;

    param_stats : [StatsDef; max_num_of_stats];
    stats_config_table : StatsConfig;

    system_data_pool_u32 : access AtomicArrayAccess<u32; sdp_num_u32_params>;
    system_data_pool_u8 : access AtomicArrayAccess<u8; sdp_num_u8_params>;

    tm_channel: access TMChannelIface;   
    a_tm_handler_pool : access Allocator<TMHandlerT>;
    tm_counter : access TMCounterIface;

    pus_service_9 : access PUSS9Iface;


    method get_PID_defined_stats_index (&priv self) -> MyResult {

        var result : MyResult = MyResult::Error;

        for i : usize in 0 .. max_num_of_stats while (result is MyResult::Error) {

            if(self->exec_tc_req_status_update.PID == self->stats_config_table.PID[i]) {

                if (self->stats_config_table.defined[i] == true) {

                    if(sys_data_pool_is_valid_PID(self->exec_tc_req_status_update.PID)) {

                        self->exec_tc_req_status_update.valid_index = i;
                        result = MyResult::Ok;
                    } else {
                        self->stats_config_table.defined[i] = false;
                    }
                }

            }
        }
        return result;
    }

    viewer get_free_index (&self, index: &mut usize) -> MyResult {

        var result : MyResult = MyResult::Error;

        for i : usize in 0 .. max_num_of_stats while (result is MyResult::Error) {

            if ((self->stats_config_table.defined[i] == false) || (sys_data_pool_is_valid_PID(self->stats_config_table.PID[i])) == false) {

                *index = i; 
                result = MyResult::Ok;
            }
        }
        return result;
    }

    viewer is_stats_index_defined(&self, index: usize) -> bool {

        var is_defined: bool = false;

        if(index < max_num_of_stats){

            if (self->stats_config_table.defined[index] == true) {

                if(sys_data_pool_is_valid_PID(self->stats_config_table.PID[index])) {

                    is_defined = true;

                } else {

                }
            }
        }

        return is_defined;
    }


    method add_PID_stats (&priv self) -> MyResult {

        var result : MyResult = MyResult::Ok;
        

        result = self->get_PID_defined_stats_index();

        if(result is MyResult::Error) {

            var index : usize = 0;
            result = self->get_free_index(&mut index);
            self->exec_tc_req_status_update.valid_index = index;
        }

        if(result is MyResult::Ok) {

            var PID_index: usize = self->exec_tc_req_status_update.valid_index;

            self->stats_config_table.defined[PID_index] = true;
            self->stats_config_table.PID[PID_index] = self->exec_tc_req_status_update.PID;

            self->param_stats[PID_index].samples = 0;
            var current_obt : MissionObt = {seconds = 0, finetime = 0};
            self->pus_service_9.get_current_obt(&mut current_obt);
            self->stats_config_table.start_time[PID_index] = current_obt;
            
        }

        return result;
    }

    method delete_PID_stats (&priv self) -> MyResult {

        var result : MyResult = MyResult::Error;
        var PID_index: usize = 0;
        result = self->get_PID_defined_stats_index();

        if (result is MyResult::Ok) {
            self->stats_config_table.defined[PID_index] = false;
        }

        return result;
        
    }

    method delete_all_stats (&priv self){

        for i : usize in 0 .. max_num_of_stats {
            self->stats_config_table.defined[i] = false;
        }

        return;
        
    }

    viewer build_tm_4_2(&self, p_tm_handler : &mut TMHandlerT, tm_seq_counter : u16, index: usize, result: &mut MyResult){

        startup_tm(p_tm_handler);

        var current_obt : MissionObt = {seconds = 0, finetime = 0};
        self->pus_service_9.get_current_obt(&mut current_obt);
        
        append_u32_appdata_field(p_tm_handler, self->stats_config_table.start_time[index].seconds, result);
        append_u32_appdata_field(p_tm_handler, current_obt.seconds, result);
        append_u8_appdata_field(p_tm_handler, self->exec_tc_req_status_update.N, result);
        append_u16_appdata_field(p_tm_handler, self->stats_config_table.PID[index], result);
        append_u32_appdata_field(p_tm_handler, self->param_stats[index].samples, result);
        append_u32_appdata_field(p_tm_handler, self->param_stats[index].max, result);
        append_u32_appdata_field(p_tm_handler, self->param_stats[index].max_obt.seconds, result);
        append_u32_appdata_field(p_tm_handler, self->param_stats[index].min, result);
        append_u32_appdata_field(p_tm_handler, self->param_stats[index].min_obt.seconds, result);
        append_u32_appdata_field(p_tm_handler, self->param_stats[index].mean_value, result);

        if(*result is MyResult::Ok) {
            close_tm(p_tm_handler, 4, 2, tm_seq_counter, current_obt);
        }

        return;
    }

// TC[4,1] : Report the parameter statistics
    method exec4_1TC(&priv self) -> PSExecTCReqStatus {

        var result : MyResult = MyResult::Ok;
        var next_status : PSExecTCReqStatus = PSExecTCReqStatus::Exit;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {
            case Some(b_tm_handler)=> {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                self->pus_service_9.get_current_obt(&mut current_obt);
                build_tm_1_3(&mut b_tm_handler, tm_count, current_obt, &mut result);

                if (result is MyResult::Ok){

                    self->tm_channel.send_tm(b_tm_handler, &mut result);

                    if (result is MyResult::Error){
                        next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                    }
                } 
                else {
                    self->a_tm_handler_pool.free(b_tm_handler);
                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                }
               
            }
            case None => {
                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        for i : usize in 0 .. max_num_of_stats while (result is MyResult::Ok) {

            if (self->stats_config_table.defined[i] == true) {

                var tm_handler2 : Option<box TMHandlerT> = None;
                self->a_tm_handler_pool.alloc(&mut tm_handler2);

                match tm_handler2 {

                    case Some(b_tm_handler2) => {

                        var tm_count : u16 = 0 : u16;
                        self->tm_counter.get_next_tm_count(&mut tm_count);

                        self->build_tm_4_2(&mut b_tm_handler2, tm_count, i, &mut result);

                        if (result is MyResult::Ok){

                            self->tm_channel.send_tm(b_tm_handler2, &mut result);

                            if (result is MyResult::Error){
                                next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                            }
                        } 
                        else {
                            self->a_tm_handler_pool.free(b_tm_handler2);
                            next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                        }
                        
                        //reset_stats_index
                        self->param_stats[i].samples = 0;
                        var current_obt_update : MissionObt = {seconds = 0, finetime = 0};
                        self->pus_service_9.get_current_obt(&mut current_obt_update);
                        self->stats_config_table.start_time[i] = current_obt_update;
                    }
                    case None => {
                        next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
                    }
                }
            }
        }

        if (result is MyResult::Ok){

            var tm_handler3 : Option<box TMHandlerT> = None;
            self->a_tm_handler_pool.alloc(&mut tm_handler3);

            match tm_handler3 {

                case Some(b_tm_handler3) => {

                    var tm_count3 : u16 = 0 : u16;
                    self->tm_counter.get_next_tm_count(&mut tm_count3);

                    self->pus_service_9.get_current_obt(&mut current_obt);
                    build_tm_1_7(&mut b_tm_handler3, tm_count3, current_obt, &mut result);

                    if (result is MyResult::Ok){

                        self->tm_channel.send_tm(b_tm_handler3, &mut result);

                        if (result is MyResult::Error){
                            next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                        }
                    } 
                    else {
                        self->a_tm_handler_pool.free(b_tm_handler3);
                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                    }
                }
                case None => {
                    next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
                }
            }

        }
        else {
            //TODO TM/TC Memory Fail -> EEPROM, Recov Action -> Reset -> Reported in Reboot
            //Generar TM[5,2]
        }


        return next_status;
    }

// TC[4,6] : Add or update parameter statistics definitions
    method exec4_6TC(&priv self) -> PSExecTCReqStatus {

        var result : MyResult = MyResult::Ok;
        var next_status : PSExecTCReqStatus = PSExecTCReqStatus::Exit;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                if (self->exec_tc_req_status_update.N != 1) {

                    self->pus_service_9.get_current_obt(&mut current_obt);
                    build_tm_1_4_num_of_instr_not_valid(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                 self->exec_tc_req_status_update.N, current_obt, &mut result);

                    if (result is MyResult::Ok){

                        self->tm_channel.send_tm(b_tm_handler, &mut result);

                        if (result is MyResult::Error){
                            next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                        }
                    } 
                    else {
                        self->a_tm_handler_pool.free(b_tm_handler);
                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                    }
                    
                }
                else {

                    if(sys_data_pool_is_valid_PID(self->exec_tc_req_status_update.PID)) {

                        result = self->add_PID_stats();

                        if (result is MyResult::Ok) {

                            self->pus_service_9.get_current_obt(&mut current_obt);
                            build_tm_1_3(&mut b_tm_handler, tm_count, current_obt, &mut result);

                            if (result is MyResult::Ok){

                                self->tm_channel.send_tm(b_tm_handler, &mut result);

                                if (result is MyResult::Error){
                                    next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                }
                            } 
                            else {
                                self->a_tm_handler_pool.free(b_tm_handler);
                                next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                            }
                           
                            var tm_handler2 : Option<box TMHandlerT> = None;
                            self->a_tm_handler_pool.alloc(&mut tm_handler2);

                            match tm_handler2 {

                                case Some(b_tm_handler2) => {

                                    var tm_count2 : u16 = 0 : u16;
                                    self->tm_counter.get_next_tm_count(&mut tm_count2);

                                    self->pus_service_9.get_current_obt(&mut current_obt);
                                    build_tm_1_7(&mut b_tm_handler2, tm_count2, current_obt, &mut result);
                                    
                                    if (result is MyResult::Ok){

                                        self->tm_channel.send_tm(b_tm_handler2, &mut result);

                                        if (result is MyResult::Error){
                                            next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                        }
                                    } 
                                    else {
                                        self->a_tm_handler_pool.free(b_tm_handler2);
                                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                                    }
                                }
                                case None => {
                                    next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
                                }
                            }


                        } else {

                            self->pus_service_9.get_current_obt(&mut current_obt);
                            build_tm_1_8_not_free_stats_config(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                             self->exec_tc_req_status_update.PID, current_obt, &mut result);

                            if (result is MyResult::Ok){

                                self->tm_channel.send_tm(b_tm_handler, &mut result);

                                if (result is MyResult::Error){
                                    next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                }
                            } 
                            else {
                                self->a_tm_handler_pool.free(b_tm_handler);
                                next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                            }
                         
                        }

                    }
                    else {

                        self->pus_service_9.get_current_obt(&mut current_obt);
                        build_tm_1_4_PID_not_valid (&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                             self->exec_tc_req_status_update.PID, current_obt, &mut result);

                        if (result is MyResult::Ok){

                            self->tm_channel.send_tm(b_tm_handler, &mut result);

                            if (result is MyResult::Error){
                                next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                            }
                        } 
                        else {
                            self->a_tm_handler_pool.free(b_tm_handler);
                            next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                        }          

                    }
                }
            }
            case None => {
                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        return next_status;
    }

// TC[4,7] : Delete parameter statistics definitions
    method exec4_7TC(&priv self) -> PSExecTCReqStatus {

        var result : MyResult = MyResult::Ok;
        var next_status : PSExecTCReqStatus = PSExecTCReqStatus::Exit;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                if (self->exec_tc_req_status_update.N != 1) {

                    self->pus_service_9.get_current_obt(&mut current_obt);
                    build_tm_1_4_num_of_instr_not_valid(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                     self->exec_tc_req_status_update.N, current_obt, &mut result);

                    if (result is MyResult::Ok){

                        self->tm_channel.send_tm(b_tm_handler, &mut result);

                        if (result is MyResult::Error){
                            next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                        }
                    } 
                    else {
                        self->a_tm_handler_pool.free(b_tm_handler);
                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                    }
                    
                }
                else {

                    if(sys_data_pool_is_valid_PID(self->exec_tc_req_status_update.PID)) {

                        result = self->delete_PID_stats();

                        if (result is MyResult::Ok) {

                            self->pus_service_9.get_current_obt(&mut current_obt);
                            build_tm_1_3(&mut b_tm_handler, tm_count, current_obt, &mut result);

                            if (result is MyResult::Ok){

                                self->tm_channel.send_tm(b_tm_handler, &mut result);

                                if (result is MyResult::Error){
                                    next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                }
                            } 
                            else {
                                self->a_tm_handler_pool.free(b_tm_handler);
                                next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                            }

                            var tm_handler2 : Option<box TMHandlerT> = None;
                            self->a_tm_handler_pool.alloc(&mut tm_handler2);

                            match tm_handler2 {

                                case Some(b_tm_handler2) => {

                                    var tm_count2 : u16 = 0 : u16;
                                    self->tm_counter.get_next_tm_count(&mut tm_count2);

                                    self->pus_service_9.get_current_obt(&mut current_obt);
                                    build_tm_1_7(&mut b_tm_handler2, tm_count2, current_obt, &mut result);

                                    if (result is MyResult::Ok){

                                        self->tm_channel.send_tm(b_tm_handler2, &mut result);

                                        if (result is MyResult::Error){
                                            next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                        }
                                    } 
                                    else {
                                        self->a_tm_handler_pool.free(b_tm_handler2);
                                        next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                                    }
                                    
                                }
                                case None => {
                                    next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
                                }
                            }


                        } else {

                            self->pus_service_9.get_current_obt(&mut current_obt);
                            build_tm_1_4_PID_stats_undefined(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                             self->exec_tc_req_status_update.PID, current_obt, &mut result);

                            if (result is MyResult::Ok){

                                self->tm_channel.send_tm(b_tm_handler, &mut result);

                                if (result is MyResult::Error){
                                    next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                                }
                            } 
                            else {
                                self->a_tm_handler_pool.free(b_tm_handler);
                                next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                            }

                        }


                    }
                    else {

                        self->pus_service_9.get_current_obt(&mut current_obt);
                        build_tm_1_4_PID_not_valid (&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                                     self->exec_tc_req_status_update.PID, current_obt, &mut result);

                        if (result is MyResult::Ok){

                            self->tm_channel.send_tm(b_tm_handler, &mut result);

                            if (result is MyResult::Error){
                                next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                            }
                        } 
                        else {
                            self->a_tm_handler_pool.free(b_tm_handler);
                            next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                        }
                    }
                }
            }
            case None => {
                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        return next_status;
    }
    

    viewer get_TC_params(&self, tc_handler : &mut TCHandlerT, subtype : &mut u8, result : &mut MyResult) -> PS4ExecTCReqStatusUpdate {

        *subtype = tc_handler->df_header.subtype;

        var tc_data : PS4ExecTCReqStatusUpdate = {
            packet_id = 0,
            packet_error_ctrl = 0,
            tc_num_bytes = 0,
            N = 0,
            PID = 0,
            valid_index = 0
        };
        
        tc_data.packet_id = tc_handler->packet_header.packet_id;
        tc_data.packet_error_ctrl = tc_handler->packet_error_ctrl;
        tc_data.tc_num_bytes = tc_handler->tc_descriptor.tc_num_bytes;

        if (*subtype == 6 || *subtype == 7) {

            *result = tc_handler_get_u8_appdata_field(tc_handler, &mut (tc_data.N));
            *result = tc_handler_get_u16_appdata_field(tc_handler, &mut (tc_data.PID));
        }
        
        return tc_data;

    }

    viewer manage_short_pack_length_error(&self) -> PSExecTCReqStatus {

        var next_status : PSExecTCReqStatus = PSExecTCReqStatus::Exit;
        var result : MyResult = MyResult::Ok;

        var current_obt : MissionObt = {seconds = 0, finetime = 0};

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                self->pus_service_9.get_current_obt(&mut current_obt);
                build_tm_1_4_short_pack_length(&mut b_tm_handler, tm_count, (self->exec_tc_req_status_update.packet_id), (self->exec_tc_req_status_update.packet_error_ctrl),
                             (self->exec_tc_req_status_update.tc_num_bytes), current_obt, &mut result);

                if (result is MyResult::Ok){

                    self->tm_channel.send_tm(b_tm_handler, &mut result);

                    if (result is MyResult::Error){
                        next_status = PSExecTCReqStatus::Failure(TM_SEND_FAILURE);
                    }
                } 
                else {
                    self->a_tm_handler_pool.free(b_tm_handler);
                    next_status = PSExecTCReqStatus::Error(BUILD_TM_ERROR);
                }
            }
            case None => {
                next_status = PSExecTCReqStatus::Failure(TM_POOL_ALLOC_FAILURE);
            }
        }

        return next_status;
    }


    procedure exec_tc (&mut self, tc_handler : &mut TCHandlerT, action_status: &mut Status<i32>){

        var subtype : u8 = 0;

        for i : usize in 0 .. 3 while (self->exec_tc_req_status is PSExecTCReqStatus::Exit == false) {

            match self->exec_tc_req_status {

                case Init => {

                    var result : MyResult = MyResult::Ok;
                    self->exec_tc_req_status_update = self->get_TC_params(tc_handler, &mut subtype, &mut result);

                    if (result is MyResult::Error) {
                        self->exec_tc_req_status = PSExecTCReqStatus::Error(TC_DATA_OUT_OF_RANGE_ERROR);
                    } else {
                        self->exec_tc_req_status = PSExecTCReqStatus::ExecTC;
                    }

                }
                case ExecTC => {

                    if (subtype == 1) {

                        self->exec_tc_req_status = self->exec4_1TC();

                    } else if (subtype == 6) {

                        self->exec_tc_req_status = self->exec4_6TC();

                    } else if (subtype == 7) {

                        self->exec_tc_req_status = self->exec4_7TC();

                    } else {

                        self->exec_tc_req_status = PSExecTCReqStatus::Error(ACCEPTANCE_ERROR);
                    }
                }
                case Error(error_code) => {

                    self->exec_tc_req_status = PSExecTCReqStatus::Exit;

                    if (error_code == ACCEPTANCE_ERROR) {

                        //Generar TM[1,4] error en la aceptacion
                        
                    } else if (error_code == BUILD_TM_ERROR) {

                        //Generar TM[1,8] error en la generacion de TM
                       
                    } else if (error_code == TC_DATA_OUT_OF_RANGE_ERROR) {

                        self->exec_tc_req_status = self->manage_short_pack_length_error();
                      
                    } else {
                        
                       
                    }

                }
                case Failure(failure_code) => {

                    *action_status = Failure(failure_code);
                    self->exec_tc_req_status = PSExecTCReqStatus::Exit;

                }
                case Exit => {
                    //Unreachable point
                }  
            }
        }

        if(self->exec_tc_req_status is PSExecTCReqStatus::Exit){

            self->exec_tc_req_status = PSExecTCReqStatus::Init;
        }

        return;
    
    }

    viewer SDP_param_higher_than_limit(&self, pid_index : usize) -> bool {

        var is_higher : bool = false;
        var PID : u16 = self->stats_config_table.PID[pid_index];
        var u8_SDP_value : u8 = 0;
        var u32_SDP_value : u32 = 0;

        var item_type : DataPoolItemType = sys_data_pool_get_item_type(PID);

        match item_type {
            case u8_t => {
                self->system_data_pool_u8.load_index(PID as usize, &mut u8_SDP_value);
                if ((u8_SDP_value as u32) > self->param_stats[pid_index].max) {
                    is_higher = true;
                }
            }
            case u32_t => {
                self->system_data_pool_u32.load_index(PID as usize, &mut u32_SDP_value);
                if (u32_SDP_value > self->param_stats[pid_index].max) {
                    is_higher = true;
                }
            }
            case NoValidPID => {
                //Error
            }
        }

        return is_higher;
    }


    viewer SDP_param_lower_than_limit(&self, pid_index : usize) -> bool {

        var is_lower : bool = false;
        var PID : u16 = self->stats_config_table.PID[pid_index];
        var u8_SDP_value : u8 = 0;
        var u32_SDP_value : u32 = 0;

        var item_type : DataPoolItemType = sys_data_pool_get_item_type(PID);

        match item_type {
            case u8_t => {
                self->system_data_pool_u8.load_index(PID as usize, &mut u8_SDP_value);
                if ((u8_SDP_value as u32) < self->param_stats[pid_index].min) {
                    is_lower = true;
                }
            }
            case u32_t => {
                self->system_data_pool_u32.load_index(PID as usize, &mut u32_SDP_value);
                if (u32_SDP_value < self->param_stats[pid_index].min) {
                    is_lower = true;
                }
            }
            case NoValidPID => {
                //Error
            }
        }

        return is_lower;
    }

    viewer get_data_pool_item(&self, PID: u16 ) -> u32 {

        var item_type : DataPoolItemType = sys_data_pool_get_item_type(PID);
        var SDP_value : u32 = 0;

        match item_type {
            case u8_t => {
                var aux_value: u8 = 0;
                self->system_data_pool_u8.load_index(PID as usize, &mut aux_value);
                SDP_value = aux_value as u32;
            }
            case u32_t => {
                self->system_data_pool_u32.load_index(PID as usize, &mut SDP_value);
            }
            case NoValidPID => {
                //Error
            }
        }
        return SDP_value;

    }


    viewer SDP_get_mean(&self, pid_index : usize) -> u32 {

        var PID : u16 = self->stats_config_table.PID[pid_index];
        var u8_SDP_value : u8 = 0;
        var u32_SDP_value : u32 = 0;
        var mean_value : u32 = 0;

        var item_type : DataPoolItemType = sys_data_pool_get_item_type(PID);

        match item_type {
            case u8_t => {
                self->system_data_pool_u8.load_index(PID as usize, &mut u8_SDP_value);
                mean_value = ((self->param_stats[pid_index].mean_value * self->param_stats[pid_index].samples) + (u8_SDP_value as u32)) / (self->param_stats[pid_index].samples + 1);   
            }
            case u32_t => {
                self->system_data_pool_u32.load_index(PID as usize, &mut u32_SDP_value);
                mean_value = ((self->param_stats[pid_index].mean_value * self->param_stats[pid_index].samples) + u32_SDP_value) / (self->param_stats[pid_index].samples + 1);
            }
            case NoValidPID => {
                //Error
            }
        }

        return mean_value;
    }

    procedure update_all_stats(&mut self) {

        for i : usize in 0 .. max_num_of_stats {

            if(self->is_stats_index_defined(i) == true) {

                var PID : u16 = self->stats_config_table.PID[i];

                if(self->param_stats[i].samples > 0){

                    if(self->SDP_param_higher_than_limit(i) == true) {

                        var system_data_pool_item : u32 = self->get_data_pool_item(PID);
                        self->param_stats[i].max = system_data_pool_item;
                        var current_time : MissionObt = {
                            seconds = 0,
                            finetime = 0
                        };
                        self->pus_service_9.get_current_obt(&mut current_time);
                        self->param_stats[i].max_obt = current_time;


                    } else if (self->SDP_param_lower_than_limit(i) == true) {

                        var system_data_pool_item : u32 = self->get_data_pool_item(PID);
                        self->param_stats[i].min = system_data_pool_item;
                        var current_time : MissionObt = {
                            seconds = 0,
                            finetime = 0
                        };
                        self->pus_service_9.get_current_obt(&mut current_time);
                        self->param_stats[i].min_obt = current_time;

                    } else {

                    }

                    self->param_stats[i].mean_value = self->SDP_get_mean(i);

                }
                else {

                    var system_data_pool_item : u32 = self->get_data_pool_item(PID);
                    self->param_stats[i].max = system_data_pool_item;
                    self->param_stats[i].min = system_data_pool_item;
                    self->param_stats[i].mean_value = system_data_pool_item;
                    var current_time : MissionObt = {
                        seconds = 0,
                        finetime = 0
                    };
                    self->pus_service_9.get_current_obt(&mut current_time);
                    self->param_stats[i].max_obt = current_time;
                    self->param_stats[i].min_obt = current_time;
                    
                    
                }

                self->param_stats[i].samples = self->param_stats[i].samples + 1;

                
            } else {
                
                self->stats_config_table.defined[i] = false;
                
            }


        }

        return;
    }


    procedure startup(&mut self){

        for i : usize in 0 .. max_num_of_stats {

            if(self->is_stats_index_defined(i) == true) {

                self->param_stats[i].samples = 0;
                var current_obt : MissionObt = {seconds = 0, finetime = 0};
                self->pus_service_9.get_current_obt(&mut current_obt);
                self->stats_config_table.start_time[i] = current_obt;

            }
            else {
                self->stats_config_table.defined[i] = false;
            }
            
        }

        return;
    }

};