/*
PUS SERVICE 12 : ON-BOARD MONITORING
*/

import service_libraries.pus_services.pus_service12.pus_service12_help;


interface PUSS12Iface {
    procedure exec_tc (&mut self, tc_handler : &mut TCHandlerT, result: &mut Result);
    procedure do_monitoring (&mut self, PMONID: u16, evID : &mut u16, fault_info : &mut FaultInfo, event_triggered: &mut bool);
    
};



resource class PUSService12 provides PUSS12Iface {

    do_monitoring_req_status : DoMonitoringReqStatus;
    do_monitoring_req_status_update : DoMonitoringReqStatusUpdate;

    exect_tc_req_status : PS12ExecTCReqStatus;
    exect_tc_req_status_update : PS12ExecTCReqStatusUpdate;

    param_mon_config_table : [ParamMonitoringConfiguration; max_num_pmon_ids];
    monitoring_transition_counter : u8; 
    param_mon_transitions_table : [ParamMonitoringTransition; max_num_transitions];

    system_data_pool_u32 : access AtomicArrayAccess<u32; sdp_num_u32_params>;
    system_data_pool_u8 : access AtomicArrayAccess<u8; sdp_num_u8_params>;

    tm_channel : access TMChannelIface;
    a_tm_handler_pool : access Allocator<TMHandlerT>;
    tm_counter : access TMCounterIface;


    viewer is_valid_PMONID(&self) -> bool{

        var is_valid : bool = false;

        if (self->do_monitoring_req_status_update.PMONID as usize < max_num_pmon_ids) {

            is_valid = true;
        }

        return is_valid;
    }

    viewer is_limits_monitoring(&self) -> bool{

        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize; 
        var is_limits_mon : bool = false;

        if (self->param_mon_config_table[current_PMON_ID].type is MonitorCheckType::Limits) {

            is_limits_mon = true;
        }

        return is_limits_mon;
    }


    viewer is_expected_value_monitoring(&self) -> bool{

        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize; 
        var is_exp_val_mon : bool = false;

        if (self->param_mon_config_table[current_PMON_ID].type is MonitorCheckType::ExpectedValue){

            is_exp_val_mon = true;
        }

        return is_exp_val_mon;
    }

    viewer get_limits_monitoring_definition(&self) -> ParamLimitCheckDefinition {

        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize; 
        var monitoring_definition : ParamLimitCheckDefinition = {
            low_limit = 0,
            low_limit_evID = 0,
            high_limit = 0,
            high_limit_evID = 0
        };

        match(self->param_mon_config_table[current_PMON_ID].definition) {

            case ParamValueCheck(param_check_defintion)=> {
                //error
            }
            case ParamLimitCheck(limit_check_def)=> {

                monitoring_definition = limit_check_def;
            }
            
            case ParamDeltaCheck(delta_check_definition) => {
                //error
            }
            case Unselected => {
                //error
            }
        }

        return monitoring_definition;
    }


    viewer get_expected_value_monitoring_definition(&self) -> ParamValueCheckDefinition {

        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize; 
        var monitoring_definition : ParamValueCheckDefinition = {
            mask_value = 0,
            expected_value = 0,
            EvID = 0
        };

        match(self->param_mon_config_table[current_PMON_ID].definition) {

            case ParamValueCheck(param_check_definition)=> {

                monitoring_definition = param_check_definition;
                
            }
            case ParamLimitCheck(limit_check_def)=> {

            }
            
            case ParamDeltaCheck(delta_check_definition) => {
                
            }
            case Unselected => {
                
            }
        }

        return monitoring_definition;
    }

    method manage_interval_control(&priv self)->DoMonitoringReqStatus{

        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize; 
        var next_status : DoMonitoringReqStatus = DoMonitoringReqStatus::Exit;

        self->param_mon_config_table[current_PMON_ID].interval_control = self->param_mon_config_table[current_PMON_ID].interval_control + 1;

        if(self->param_mon_config_table[current_PMON_ID].interval_control >= self->param_mon_config_table[current_PMON_ID].interval){

            self->param_mon_config_table[current_PMON_ID].interval_control = 0;
            next_status = DoMonitoringReqStatus::GetMonitoringType;

        } else {

            next_status = DoMonitoringReqStatus::Exit;
        }

        return next_status;
    }

    viewer check_PID_status_limits_monitoring(&self)-> CheckLimitsStatus{

        var current_monitor_definition : ParamLimitCheckDefinition = self->get_limits_monitoring_definition();
        var check_status: CheckLimitsStatus = CheckLimitsStatus::MonitorUnchecked;

        if(self->PID_is_above_upper_limit(&current_monitor_definition)){

            check_status = CheckLimitsStatus::MonitorAboveHighLimit;

        }
        else if(self->PID_is_below_lower_limit(&current_monitor_definition)){

            check_status = CheckLimitsStatus::MonitorBelowLowLimit;

        }
        else {

            //monitor within limits
            check_status = CheckLimitsStatus::MonitorWithinLimits;
        }

        return check_status;

    }

    viewer check_PID_status_exp_val_monitoring(&self)-> CheckValueStatus {

        var current_monitor_definition : ParamValueCheckDefinition = self->get_expected_value_monitoring_definition();
        var check_status : CheckValueStatus = CheckValueStatus::MonitorUnchecked;

        if(self->PID_has_expected_masked_value(&current_monitor_definition)){

            check_status = CheckValueStatus::MonitorValueExpected;

        }
        else {

            check_status = CheckValueStatus::MonitorValueUnexpected;

        }

        return check_status;
    }



    viewer PID_is_above_upper_limit(&self, limit_check_def : &ParamLimitCheckDefinition) -> bool {

        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize; 
        var res : bool = false;
        var upper_limit : u32 = limit_check_def->high_limit;
        var PID : u16 = self->param_mon_config_table[current_PMON_ID].PID;
        var type : DataPoolItemType = sys_data_pool_get_item_type(PID);
        var SDP_value : u32 = 0;

        match type {

            case u32_t => {
                self->system_data_pool_u32.load_index((PID as usize - sdp_first_u32_param_id), &mut SDP_value);
            }
            case u8_t => {
                var SDP_value_u8 : u8 = 0;
                self->system_data_pool_u8.load_index((PID as usize - sdp_first_u8_param_id), &mut SDP_value_u8);
                SDP_value = SDP_value_u8 as u32;
            }
            case NoValidPID => {
               //Error
            }
        }
        
        if (SDP_value > upper_limit) {

            res = true;
        }

        return res;
    }


   viewer PID_is_below_lower_limit(&self, limit_check_def : &ParamLimitCheckDefinition) -> bool {

        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize; 
        var res : bool = false;
        var lower_limit : u32 = limit_check_def->low_limit;
        var PID : u16 = self->param_mon_config_table[current_PMON_ID].PID;
        var type : DataPoolItemType = sys_data_pool_get_item_type(PID);
        var SDP_value : u32 = 0;

        match type {

            case u32_t => {
                self->system_data_pool_u32.load_index((PID as usize - sdp_first_u32_param_id), &mut SDP_value);
            }
            case u8_t => {
                var SDP_value_u8 : u8 = 0;
                self->system_data_pool_u8.load_index(PID as usize - sdp_first_u8_param_id, &mut SDP_value_u8);
                SDP_value = SDP_value_u8 as u32;
            }
            case NoValidPID => {
               //Error
            }
        }
        
        if (SDP_value < lower_limit) {

            res = true;
        }

        return res;
    }

    
    viewer PID_has_expected_masked_value(&self, expected_value_check_definition : &ParamValueCheckDefinition) -> bool {

        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize;
        var res : bool = false;
        var PID : u16 = self->param_mon_config_table[current_PMON_ID].PID;
        var SDP_value : u32 = 0;
        var type : DataPoolItemType = sys_data_pool_get_item_type(PID);

        match type {

            case u32_t => {
                self->system_data_pool_u32.load_index((PID as usize - sdp_first_u32_param_id), &mut SDP_value);
            }
            case u8_t => {
                var SDP_value_u8 : u8 = 0;
                self->system_data_pool_u8.load_index((PID as usize - sdp_first_u8_param_id), &mut SDP_value_u8);
                SDP_value = SDP_value_u8 as u32;
            }
            case NoValidPID => {
               //Error
            }
        }

        if ((SDP_value & expected_value_check_definition->mask_value) == (expected_value_check_definition->expected_value & expected_value_check_definition->mask_value)) {

            res = true;
        }

        return res;
    }


    method manage_unexpected_value (&priv self) -> DoMonitoringReqStatus {

        var next_status : DoMonitoringReqStatus = DoMonitoringReqStatus::GetRequestStatusUpdate;

        var fault_info : ParamFaultValueInfo = {PID = 0,
                                                PID_value = 0,
                                                PID_mask = 0,
                                                PID_expected_value = 0};

        let check_status : CheckValueStatus = CheckValueStatus::MonitorValueUnexpected;
        var new_status : CheckState = CheckState::ParamValueStatus(check_status);
        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize; 

        match(self->param_mon_config_table[current_PMON_ID].definition){

            case ParamValueCheck(check_definition) => {

                fault_info.PID = self->param_mon_config_table[current_PMON_ID].PID; 
                fault_info.PID_expected_value = check_definition.expected_value;
                fault_info.PID_mask = check_definition.mask_value;

                var type : DataPoolItemType = sys_data_pool_get_item_type(fault_info.PID);

                match type {

                    case u32_t => {
                        self->system_data_pool_u32.load_index((fault_info.PID as usize - sdp_first_u32_param_id), &mut (fault_info.PID_value));
                    }
                    case u8_t => {
                        var value : u8 = 0;
                        self->system_data_pool_u8.load_index((fault_info.PID as usize - sdp_first_u8_param_id), &mut value);
                        fault_info.PID_value = value as u32;
                        
                    }
                    case NoValidPID => {
                    //Error
                    }
                }

                if(self->manage_new_status()){

                    self->do_monitoring_req_status_update.event_triggered = true;
                    self->do_monitoring_req_status_update.fault_info = FaultInfo::ParamFaultValue(fault_info);
                    self->add_monitoring_transition();
                    self->do_monitoring_req_status_update.EvID = check_definition.EvID;
                }

            }
            case ParamLimitCheck(check_definition) => {

            }
            case ParamDeltaCheck(check_definition) => {

            }
            case Unselected => {
                
            }
        }

        return next_status;
    }


    method manage_expected_value (&priv self) -> DoMonitoringReqStatus {

        var next_status : DoMonitoringReqStatus = DoMonitoringReqStatus::GetRequestStatusUpdate;

        let check_status : CheckValueStatus = CheckValueStatus::MonitorValueExpected;
        var new_status : CheckState = CheckState::ParamValueStatus(check_status); 

        if(self->manage_new_status()){

            self->add_monitoring_transition();

        }
        return next_status;
    }



    method manage_param_above_upper_limit (&priv self) -> DoMonitoringReqStatus {

        var next_status : DoMonitoringReqStatus = DoMonitoringReqStatus::GetRequestStatusUpdate;

        var fault_info : ParamOutOfLimitInfo = {PID = 0,
                                                PID_value = 0,
                                                PID_limit = 0};

        let check_status : CheckLimitsStatus = CheckLimitsStatus:: MonitorAboveHighLimit;
        self->do_monitoring_req_status_update.new_status = CheckState::ParamLimitStatus(check_status);

        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize; 

        match(self->param_mon_config_table[current_PMON_ID].definition){

            case ParamValueCheck(check_definition) => {

            }
            case ParamLimitCheck(check_definition) => {

                fault_info.PID = self->param_mon_config_table[current_PMON_ID].PID; 
                fault_info.PID_limit = check_definition.high_limit;

                var type : DataPoolItemType = sys_data_pool_get_item_type(fault_info.PID);

                match type {

                    case u32_t => {
                        self->system_data_pool_u32.load_index((fault_info.PID as usize - sdp_first_u32_param_id), &mut (fault_info.PID_value));
                    }
                    case u8_t => {
                        var value : u8 = 0;
                        self->system_data_pool_u8.load_index((fault_info.PID as usize - sdp_first_u8_param_id), &mut value);
                        fault_info.PID_value = value as u32;
                    }
                    case NoValidPID => {
                    //Error
                    }
                }

                if(self->manage_new_status()){

                    self->do_monitoring_req_status_update.event_triggered = true;
                    self->do_monitoring_req_status_update.fault_info = FaultInfo::ParamOutOfLimit(fault_info);
                    self->add_monitoring_transition();
                    self->do_monitoring_req_status_update.EvID = check_definition.high_limit_evID;
                }

            }
            case ParamDeltaCheck(check_definition) => {

            }
            case Unselected => {
                
            }
        }

        return next_status;
    }


    method manage_param_below_lower_limit (&priv self) -> DoMonitoringReqStatus {

        var next_status : DoMonitoringReqStatus = DoMonitoringReqStatus::GetRequestStatusUpdate;

        var fault_info : ParamOutOfLimitInfo = {PID = 0,
                                                PID_value = 0,
                                                PID_limit = 0};

        let check_status : CheckLimitsStatus = CheckLimitsStatus:: MonitorBelowLowLimit;
        self->do_monitoring_req_status_update.new_status = CheckState::ParamLimitStatus(check_status);

        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize; 

        match(self->param_mon_config_table[current_PMON_ID].definition){

            case ParamValueCheck(check_definition) => {

            }
            case ParamLimitCheck(check_definition) => {

                fault_info.PID = self->param_mon_config_table[current_PMON_ID].PID; 
                fault_info.PID_limit = check_definition.low_limit;

                var type : DataPoolItemType = sys_data_pool_get_item_type(fault_info.PID);

                match type {

                    case u32_t => {
                        self->system_data_pool_u32.load_index((fault_info.PID as usize - sdp_first_u32_param_id), &mut (fault_info.PID_value));
                    }
                    case u8_t => {
                        var value : u8 = 0;
                        self->system_data_pool_u8.load_index((fault_info.PID as usize - sdp_first_u8_param_id), &mut value);
                        fault_info.PID_value = value as u32;
                    }
                    case NoValidPID => {
                    //Error
                    }
                }

                if(self->manage_new_status()){

                    self->do_monitoring_req_status_update.event_triggered = true;
                    self->do_monitoring_req_status_update.fault_info = FaultInfo::ParamOutOfLimit(fault_info);
                    self->add_monitoring_transition();
                    self->do_monitoring_req_status_update.EvID = check_definition.low_limit_evID;
                }

            }
            case ParamDeltaCheck(check_definition) => {

            }
            case Unselected => {
                
            }
        }

        return next_status;
    }

    method manage_param_within_limits (&priv self) -> DoMonitoringReqStatus{

        var next_status : DoMonitoringReqStatus = DoMonitoringReqStatus::GetRequestStatusUpdate;

        var within_limits : CheckLimitsStatus = CheckLimitsStatus::MonitorWithinLimits;
        var new_status : CheckState = CheckState::ParamLimitStatus(within_limits);
        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize;
        let current_status : CheckState = self->param_mon_config_table[current_PMON_ID].current_state;

        if(self->are_status_equal(new_status, current_status) == false){

            if(self->manage_new_status()){

                self->add_monitoring_transition();
            }
        }

        return next_status;
    }

    viewer are_status_equal(&self, status1: CheckState, status2: CheckState)->bool{

        var equal : bool = get_check_status_index(status1) == get_check_status_index(status2);
        return equal;
    }


    method manage_new_status(&priv self)->bool{

        var transition: bool = false;
        let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize;

        var new_state : CheckState = self->do_monitoring_req_status_update.new_status;
        var current_state : CheckState = self->param_mon_config_table[current_PMON_ID].current_state;
        var temp_state : CheckState = self->param_mon_config_table[current_PMON_ID].temp_state;

        if (self->are_status_equal(new_state, current_state) == false) {

            if(are_status_equal(new_state, temp_state)) {

                self->param_mon_config_table[current_PMON_ID].repetition_control = self->param_mon_config_table[current_PMON_ID].repetition_control + 1;

            } else {

                self->param_mon_config_table[current_PMON_ID].temp_state = new_state;
                self->param_mon_config_table[current_PMON_ID].repetition_control = 1;
                //self->param_mon_config_table[current_PMON_ID].transition_obt = get_current_time();
            }

            if (self->param_mon_config_table[current_PMON_ID].repetition_control >= self->param_mon_config_table[current_PMON_ID].repetition) {

                self->param_mon_config_table[current_PMON_ID].repetition_control = 0;
                self->param_mon_config_table[current_PMON_ID].current_state = new_state;
                transition = true;

            }
        } 
        else {
            
            self->param_mon_config_table[current_PMON_ID].temp_state = new_state;
            self->param_mon_config_table[current_PMON_ID].repetition_control = 0;

        }

        return transition;
        
    }

    viewer build_tm_12_12(&self, p_tm_handler : &mut TMHandlerT, tm_seq_counter : u16, result: &mut Result){

        startup_tm(p_tm_handler);
        append_u8_appdata_field(p_tm_handler, self->monitoring_transition_counter, result);

        for i : usize in 0 .. max_num_transitions while (i < (self->monitoring_transition_counter as usize)) {

            var aux_prev_status : u8 = 0;
            var aux_new_status : u8 = 0;

            append_u16_appdata_field(p_tm_handler, self->param_mon_transitions_table[i].PMONID, result);
            append_u16_appdata_field(p_tm_handler, self->param_mon_transitions_table[i].PID, result);
            var type_id : u8 = get_type_index(self->param_mon_transitions_table[i].type);
            append_u8_appdata_field(p_tm_handler, type_id, result);
            
            
            if(self->param_mon_transitions_table[i].type is MonitorCheckType::ExpectedValue){
                append_u32_appdata_field(p_tm_handler, self->param_mon_transitions_table[i].mask_value, result);
            }

            aux_prev_status = get_check_status_index(self->param_mon_transitions_table[i].prev_status);
            aux_new_status = get_check_status_index(self->param_mon_transitions_table[i].new_status);

            append_u32_appdata_field(p_tm_handler, self->param_mon_transitions_table[i].new_value, result);
            append_u32_appdata_field(p_tm_handler, self->param_mon_transitions_table[i].limit_value, result);
            append_u8_appdata_field(p_tm_handler, aux_prev_status, result);
            append_u8_appdata_field(p_tm_handler, aux_new_status, result);
            //append_u32_appdata_field(p_tm_handler, self->param_mon_transitions_table[i].trans_obt, result); append_Timeval or convet_Timeval_to_u32??
        }
        if(*result is Result::Ok){
            close_tm(p_tm_handler, 12, 12, tm_seq_counter, result);
        }


        return;
    }


    method add_monitoring_transition(&priv self){

        var result : Result = Result::Ok;

        if((self->monitoring_transition_counter as usize) < max_num_transitions){

            let current_PMON_ID : usize = (self->do_monitoring_req_status_update.PMONID) as usize;

            self->param_mon_transitions_table[self->monitoring_transition_counter as usize].PID = self->param_mon_config_table[current_PMON_ID].PID;
            self->param_mon_transitions_table[self->monitoring_transition_counter as usize].PMONID = self->do_monitoring_req_status_update.PMONID;
            self->param_mon_transitions_table[self->monitoring_transition_counter as usize].type = self->param_mon_config_table[current_PMON_ID].type;
            match self->do_monitoring_req_status_update.fault_info {
                case ParamOutOfLimit(out_of_limit_info)=> {
                    self->param_mon_transitions_table[self->monitoring_transition_counter as usize].limit_value = out_of_limit_info.PID_limit;
                    self->param_mon_transitions_table[self->monitoring_transition_counter as usize].new_value = out_of_limit_info.PID_value;
                }
                case ParamFaultValue(fault_value_info)=> {
                    self->param_mon_transitions_table[self->monitoring_transition_counter as usize].limit_value = fault_value_info.PID_expected_value;
                    self->param_mon_transitions_table[self->monitoring_transition_counter as usize].new_value = fault_value_info.PID_value;

                }
            }
             
            //Here it would be interesting to incorporate the I don't care option so that to improve code conciseness
            match (self->param_mon_config_table[current_PMON_ID].definition) {
                case ParamValueCheck(value_definition)=> {

                     self->param_mon_transitions_table[self->monitoring_transition_counter as usize].mask_value = value_definition.mask_value;

                }
                case ParamLimitCheck(limit_definition)=> {

                }
                case ParamDeltaCheck(delta_definition)=> {

                }
                case Unselected => {
                    //error
                }

            }

            self->param_mon_transitions_table[self->monitoring_transition_counter as usize].new_status = self->do_monitoring_req_status_update.new_status;
            self->param_mon_transitions_table[self->monitoring_transition_counter as usize].prev_status = self->param_mon_config_table[current_PMON_ID].current_state;
            self->param_mon_transitions_table[self->monitoring_transition_counter as usize].trans_obt = self->param_mon_config_table[current_PMON_ID].transition_obt;

            self->monitoring_transition_counter = self->monitoring_transition_counter + 1;
        }

        if(self->monitoring_transition_counter as usize == max_num_transitions){

            var tm_handler : Option<box TMHandlerT> = None;
            self->a_tm_handler_pool.alloc(&mut tm_handler);

            match tm_handler {

                case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                self->build_tm_12_12(&mut b_tm_handler, tm_count, &mut result);
                //Enviar telemetría a través del TM_CHANNEL
                self->tm_channel.send_tm(b_tm_handler, &mut result);
            
                }
                case None => {
                    result = Result::Error;

                }
            } 

            
            self->monitoring_transition_counter = 0;
        }

        return;
    }

    procedure do_monitoring (&mut self, PMONID: u16, evID : &mut u16, fault_info : &mut FaultInfo, event_triggered: &mut bool) {

        for i : usize in 0 .. 4 while (self->do_monitoring_req_status is DoMonitoringReqStatus::Exit == false){
            match self->do_monitoring_req_status {
                case Init=> {

                    self->do_monitoring_req_status_update.PMONID = PMONID;
                    self->do_monitoring_req_status_update.EvID = *evID;
                    self->do_monitoring_req_status_update.fault_info = *fault_info;
                    self->do_monitoring_req_status = DoMonitoringReqStatus::CheckPMONID;
                    self->do_monitoring_req_status_update.event_triggered = false;

                }
                case CheckPMONID => {

                    //choice point
                    if (self->is_valid_PMONID()) {
                        self->do_monitoring_req_status = self->manage_interval_control(); //method 
                    } else {
                        self->do_monitoring_req_status = DoMonitoringReqStatus::Exit;
                    }
                }
                
                case GetMonitoringType => {

                    if(self->param_mon_config_table[PMONID as usize].enabled == true){

                        //choice point
                        if(self->is_limits_monitoring()) {

                            self->do_monitoring_req_status = DoMonitoringReqStatus::DoLimitsMonitoring;
                            

                        } else if (self->is_expected_value_monitoring()) {

                            self->do_monitoring_req_status = DoMonitoringReqStatus::DoExpectedValueMonitoring;

                        }
                        else {
                            self->do_monitoring_req_status = DoMonitoringReqStatus::Exit;
                        }

                    } else {

                        self->do_monitoring_req_status = DoMonitoringReqStatus::Exit;
                    }
                
                } 
                case DoLimitsMonitoring =>{

                    var limits_monitoring_status : CheckLimitsStatus = self->check_PID_status_limits_monitoring(); 

                    match limits_monitoring_status {
                        case MonitorAboveHighLimit => {

                            self->do_monitoring_req_status = self->manage_param_above_upper_limit(); //method
                        }
                        case MonitorBelowLowLimit => {

                            self->do_monitoring_req_status = self->manage_param_below_lower_limit(); //method

                        }
                        case MonitorWithinLimits => {

                            self->do_monitoring_req_status = self->manage_param_within_limits(); //method
                        }
                        case MonitorUnchecked => {

                        }

                    }
                        
                }
                case DoExpectedValueMonitoring => {

                    var exp_value_monitoring_status: CheckValueStatus = self->check_PID_status_exp_val_monitoring(); //viewer

                    match exp_value_monitoring_status {
                        case MonitorValueUnexpected =>{
                            self->do_monitoring_req_status = self->manage_unexpected_value(); //method
                        }
                        case MonitorValueExpected =>{

                            self->do_monitoring_req_status = self->manage_expected_value(); //method

                        }
                        case MonitorUnchecked => {

                        }
                        

                    }
                }
                case GetRequestStatusUpdate => {
                    *evID = self->do_monitoring_req_status_update.EvID;
                    *fault_info = self->do_monitoring_req_status_update.fault_info;
                }
                case Exit => {
                    //error
                }
            }
        }

        if(self->do_monitoring_req_status is DoMonitoringReqStatus::Exit){

            self->do_monitoring_req_status = DoMonitoringReqStatus::Init;
        }

        return;
    }


    viewer get_PMON_type (&self, PMONID : usize) -> MonitorCheckType {

        var mon_type : MonitorCheckType = MonitorCheckType::Free;

        if (PMONID < max_num_pmon_ids) {

            mon_type = self->param_mon_config_table[PMONID as usize].type;
        }

        return mon_type;
    }

    method set_unchecked (&priv self) {

        var valid_PMONID : usize = (self->exect_tc_req_status_update. tc_data_1_2_6.PMONID) as usize;

        match (self->param_mon_config_table[valid_PMONID].current_state) {

            case ParamValueStatus(check_val) => {
                var monitor_unchecked : CheckValueStatus = CheckValueStatus::MonitorUnchecked;
                self->param_mon_config_table[valid_PMONID].current_state = CheckState::ParamValueStatus(monitor_unchecked);
            }
            case ParamLimitStatus(check_limits) => {
                var monitor_unchecked : CheckLimitsStatus = CheckLimitsStatus::MonitorUnchecked;
                self->param_mon_config_table[valid_PMONID].current_state = CheckState::ParamLimitStatus(monitor_unchecked);
            }
            case ParamDeltaStatus(check_delta) => {
                var monitor_unchecked : CheckDeltaStatus = CheckDeltaStatus::MonitorUnchecked;
                self->param_mon_config_table[valid_PMONID].current_state = CheckState::ParamDeltaStatus(monitor_unchecked);
            }
            case Unselected => {
                //Do nothing
            }
        }

        self->param_mon_config_table[valid_PMONID].temp_state = self->param_mon_config_table[valid_PMONID].current_state;

        return;
        }

    method exec12_1TC(&priv self) -> PS12ExecTCReqStatus {

        var result : Result = Result::Ok;
        var next_status : PS12ExecTCReqStatus = PS12ExecTCReqStatus::Error;

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                if (self->exect_tc_req_status_update.tc_data_1_2_6.N != 1) {

                    build_tm_1_4_num_of_instr_not_valid(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update. tc_data_1_2_6.N, &mut result);
                    self->tm_channel.send_tm(b_tm_handler, &mut result);
                }
                else {

                    var PMONID : usize = (self->exect_tc_req_status_update.tc_data_1_2_6.PMONID) as usize;
                    var is_valid_PMONID : bool = PMONID < max_num_pmon_ids;

                    if (is_valid_PMONID) {

                        if(self->param_mon_config_table[PMONID as usize].type is MonitorCheckType::Free == false){

                            build_tm_1_3(&mut b_tm_handler, tm_count, &mut result);
                            self->tm_channel.send_tm(b_tm_handler, &mut result);
                            self->param_mon_config_table[PMONID as usize].enabled = true;
                            self->param_mon_config_table[PMONID as usize].interval_control = 0;
                            self->param_mon_config_table[PMONID as usize].repetition_control = 0;

                            var tm_handler2 : Option<box TMHandlerT> = None;
                            self->a_tm_handler_pool.alloc(&mut tm_handler2);

                            match tm_handler2 {

                                case Some(b_tm_handler2) => {

                                    var tm_count2 : u16 = 0 : u16;
                                    self->tm_counter.get_next_tm_count(&mut tm_count2);
                                    build_tm_1_7(&mut b_tm_handler2, tm_count2, &mut result);
                                    self->tm_channel.send_tm(b_tm_handler2, &mut result);
                                }
                                case None => {
                                    result = Result::Error;
                                }
                            }

                        } else {

                            build_tm_1_4_PMON_undefined(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update. tc_data_1_2_6.PMONID, &mut result);
                            self->tm_channel.send_tm(b_tm_handler, &mut result);
                        }
                    } else {

                        build_tm_1_4_PMONID_invalid(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update. tc_data_1_2_6.PMONID, &mut result);
                        self->tm_channel.send_tm(b_tm_handler, &mut result);
                    }
                }
                    
            }
            case None => {
                result = Result::Error;
            }
        }

        if (result is Result::Error){

            next_status = PS12ExecTCReqStatus::Error;

        } else {

            next_status = PS12ExecTCReqStatus::Exit;

        }

        return next_status;
    }


    method exec12_2TC(&priv self) -> PS12ExecTCReqStatus {

        var result : Result = Result::Ok;
        var next_status : PS12ExecTCReqStatus = PS12ExecTCReqStatus::Error;
        
        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

               
                if (self->exect_tc_req_status_update.tc_data_1_2_6.N != 1) {

                    build_tm_1_4_num_of_instr_not_valid(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update.tc_data_1_2_6.N, &mut result);
                    self->tm_channel.send_tm(b_tm_handler, &mut result);
                }
                else {

                    var is_valid_PMONID : bool = (self->exect_tc_req_status_update.tc_data_1_2_6.PMONID) as usize < max_num_pmon_ids;

                    if (is_valid_PMONID) {

                        if(self->param_mon_config_table[(self->exect_tc_req_status_update.tc_data_1_2_6.PMONID) as usize].type is MonitorCheckType::Free == false){

                            build_tm_1_3(&mut b_tm_handler, tm_count, &mut result);
                            self->tm_channel.send_tm(b_tm_handler, &mut result);
                            self->param_mon_config_table[(self->exect_tc_req_status_update.tc_data_1_2_6.PMONID) as usize].enabled = false;
                            self->set_unchecked();
                            var tm_handler2 : Option<box TMHandlerT> = None;
                            self->a_tm_handler_pool.alloc(&mut tm_handler2);

                            match tm_handler2 {

                                case Some(b_tm_handler2) => {

                                    var tm_count2 : u16 = 0 : u16;
                                    self->tm_counter.get_next_tm_count(&mut tm_count2);
                                    build_tm_1_7(&mut b_tm_handler2, tm_count2, &mut result);
                                    self->tm_channel.send_tm(b_tm_handler2, &mut result);
                                }
                                case None => {
                                    result = Result::Error;
                                }
                            }

                        } else {

                            build_tm_1_4_PMON_undefined(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update. tc_data_1_2_6.PMONID, &mut result);
                            self->tm_channel.send_tm(b_tm_handler, &mut result);
                        }
                    } else {

                        build_tm_1_4_PMONID_invalid(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update. tc_data_1_2_6.PMONID, &mut result);
                        self->tm_channel.send_tm(b_tm_handler, &mut result);
                    }
                }
                
            }
            case None => {
                result = Result::Error;
            }
        }

        if (result is Result::Error){

            next_status = PS12ExecTCReqStatus::Error;

        } else {

            next_status = PS12ExecTCReqStatus::Exit;

        }

        return next_status;
    }

    method add_valid_mng_mon_def (&priv self){

        let PMONID : usize = (self->exect_tc_req_status_update.tc_data_5.mon_config.PID) as usize;

        (self->param_mon_config_table)[PMONID] = self->exect_tc_req_status_update.tc_data_5.mon_config;

        (self->param_mon_config_table)[PMONID].enabled = false;

        //set unckecked
        if (PMONID < max_num_pmon_ids){

            match (self->param_mon_config_table[PMONID].type) {

                case ExpectedValue => {

                    var status : CheckValueStatus = CheckValueStatus::MonitorUnchecked;
                    (self->param_mon_config_table)[PMONID].current_state = CheckState::ParamValueStatus(status);
                }
                case Limits => {

                    var status : CheckLimitsStatus = CheckLimitsStatus::MonitorUnchecked;
                    (self->param_mon_config_table)[PMONID].current_state = CheckState::ParamLimitStatus(status);

                }
                case Delta => { 

                    var status : CheckDeltaStatus = CheckDeltaStatus::MonitorUnchecked;
                    (self->param_mon_config_table)[PMONID].current_state = CheckState::ParamDeltaStatus(status);

                }
                case Free => {

                    //Error

                }    
    
            }

        }

        return;
    }

    method exec12_5TC(&priv self) -> PS12ExecTCReqStatus{

        var result : Result = Result::Ok;
        var next_status : PS12ExecTCReqStatus = PS12ExecTCReqStatus::Error;

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

         match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                if (self->exect_tc_req_status_update.tc_data_5.N != 1) {

                    build_tm_1_4_num_of_instr_not_valid(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update.tc_data_5.N, &mut result);
                    self->tm_channel.send_tm(b_tm_handler, &mut result);

                } else if (sys_data_pool_is_valid_PID(self->exect_tc_req_status_update.tc_data_5.mon_config.PID) == false) {

                    build_tm_1_4_PID_not_valid(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update.tc_data_5.mon_config.PID, &mut result);
                    self->tm_channel.send_tm(b_tm_handler, &mut result);

                } else if (self->exect_tc_req_status_update.tc_data_5.mon_config.type is MonitorCheckType::Limits) {

                    if(is_valid_check_limit_def(&self->exect_tc_req_status_update.tc_data_5.mon_config.definition)){

                        build_tm_1_3(&mut b_tm_handler, tm_count, &mut result);
                        self->tm_channel.send_tm(b_tm_handler, &mut result);
                        self->add_valid_mng_mon_def();
            
                        var tm_handler2 : Option<box TMHandlerT> = None;
                        self->a_tm_handler_pool.alloc(&mut tm_handler2);

                        match tm_handler2 {

                            case Some(b_tm_handler2) => {

                                var tm_count2 : u16 = 0 : u16;
                                self->tm_counter.get_next_tm_count(&mut tm_count2);
                                build_tm_1_7(&mut b_tm_handler2, tm_count2, &mut result);
                                self->tm_channel.send_tm(b_tm_handler2, &mut result);
                            }
                            case None => {
                                result = Result::Error;
                            }
                        
                        }
                    } else {

                        build_tm_1_4_PMON_definition_invalid(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                    (self->exect_tc_req_status_update.packet_error_ctrl), (self->exect_tc_req_status_update.tc_data_5.PMONID), &mut result);
                        self->tm_channel.send_tm(b_tm_handler, &mut result);
                    }

                } else if (self->exect_tc_req_status_update.tc_data_5.mon_config.type is MonitorCheckType::ExpectedValue) {

                    build_tm_1_3(&mut b_tm_handler, tm_count, &mut result);
                    self->tm_channel.send_tm(b_tm_handler, &mut result);
                    self->add_valid_mng_mon_def();

                    var tm_handler2 : Option<box TMHandlerT> = None;
                    self->a_tm_handler_pool.alloc(&mut tm_handler2);

                    match tm_handler2 {

                        case Some(b_tm_handler2) => {

                            var tm_count2 : u16 = 0 : u16;
                            self->tm_counter.get_next_tm_count(&mut tm_count2);
                            build_tm_1_7(&mut b_tm_handler2, tm_count2, &mut result);
                            self->tm_channel.send_tm(b_tm_handler2, &mut result);
                        }
                        case None => {
                            result = Result::Error;
                        }
                    }
                } else {

                    build_tm_1_4_PMON_definition_invalid(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), (self->exect_tc_req_status_update.tc_data_5.PMONID), &mut result);
                    self->tm_channel.send_tm(b_tm_handler, &mut result);
                }
            }
            case None => {
                result = Result::Error;
            }
        }

        if (result is Result::Error){

            next_status = PS12ExecTCReqStatus::Error;

        } else {

            next_status = PS12ExecTCReqStatus::Exit;

        }

        return next_status;

    }

//Comprobar exec12_6TC enteramente
    method exec12_6TC(&priv self) -> PS12ExecTCReqStatus {

        var result : Result = Result::Ok;
        var next_status : PS12ExecTCReqStatus = PS12ExecTCReqStatus::Error;

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);


         match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                if (self->exect_tc_req_status_update.tc_data_1_2_6.N != 1) {

                    build_tm_1_4_num_of_instr_not_valid(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update.tc_data_1_2_6.N, &mut result);
                    self->tm_channel.send_tm(b_tm_handler, &mut result);
                }
                else {
                    
                    var PMON_type : MonitorCheckType = get_check_type((self->exect_tc_req_status_update.tc_data_1_2_6.PMONID) as u8);

                    if(PMON_type is MonitorCheckType::Free){

                        build_tm_1_4_PMON_undefined(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update.tc_data_1_2_6.PMONID, &mut result);
                        self->tm_channel.send_tm(b_tm_handler, &mut result);

                    } else if (self->param_mon_config_table[(self->exect_tc_req_status_update.tc_data_1_2_6.PMONID)as usize].enabled == false){ 

                        build_tm_1_3(&mut b_tm_handler, tm_count, &mut result);
                        self->tm_channel.send_tm(b_tm_handler, &mut result);
                      
                        self->param_mon_config_table[(self->exect_tc_req_status_update.tc_data_1_2_6.PMONID) as usize].type = MonitorCheckType::Free;

                        var tm_handler2 : Option<box TMHandlerT> = None;
                        self->a_tm_handler_pool.alloc(&mut tm_handler2);

                        match tm_handler2 {

                            case Some(b_tm_handler2) => {

                                var tm_count2 : u16 = 0 : u16;
                                self->tm_counter.get_next_tm_count(&mut tm_count2);
                                build_tm_1_7(&mut b_tm_handler2, tm_count2, &mut result);
                                self->tm_channel.send_tm(b_tm_handler2, &mut result);
                            }
                            case None => {
                                result = Result::Error;
                            }
                        }
                    } else {

                        build_tm_1_4_PMON_enabled(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                                        (self->exect_tc_req_status_update.packet_error_ctrl), self->exect_tc_req_status_update.tc_data_1_2_6.PMONID, &mut result);
                        self->tm_channel.send_tm(b_tm_handler, &mut result);
                    }

                }


            } case None => {

                result = Result::Error;
            }
        }

        if (result is Result::Error){

            next_status = PS12ExecTCReqStatus::Error;

        } else {

            next_status = PS12ExecTCReqStatus::Exit;

        }

        return next_status;

    }

    viewer manage_short_pack_length_error(&self) -> PS12ExecTCReqStatus {

        var next_status : PS12ExecTCReqStatus = PS12ExecTCReqStatus::Error;
        var result : Result = Result::Ok;

        var tm_handler : Option<box TMHandlerT> = None;
        self->a_tm_handler_pool.alloc(&mut tm_handler);

        match tm_handler {

            case Some(b_tm_handler) => {

                var tm_count : u16 = 0 : u16;
                self->tm_counter.get_next_tm_count(&mut tm_count);

                build_tm_1_4_short_pack_length(&mut b_tm_handler, tm_count, (self->exect_tc_req_status_update.packet_id), 
                                            (self->exect_tc_req_status_update.packet_error_ctrl), (self->exect_tc_req_status_update.tc_num_bytes), &mut result);
                self->tm_channel.send_tm(b_tm_handler, &mut result);
            }
            case None => {
                result = Result::Error;
            }
        }

        if (result is Result::Error){

            next_status = PS12ExecTCReqStatus::Error;

        } else {

            next_status = PS12ExecTCReqStatus::Exit;

        }

        return next_status;
    }


    viewer get_PMON_limit_check_definition (&self, tc_handler : &mut TCHandlerT, result: &mut Result) -> MonitorDefinition {

        var limits_def : ParamLimitCheckDefinition = {
            low_limit = 0,
            low_limit_evID = 0,
            high_limit = 0,
            high_limit_evID = 0
        };

        var type : DataPoolItemType = sys_data_pool_get_item_type(self->exect_tc_req_status_update.tc_data_5.mon_config.PID);

        match type {

            case u8_t => {

                var low_limit : u8 = 0;
                var high_limit : u8 = 0;

                //get low limit
                *result = tc_handler_get_u8_appdata_field(tc_handler, &mut low_limit);

                //get low limit evID
                *result = tc_handler_get_u16_appdata_field(tc_handler, &mut (limits_def.low_limit_evID));

                //get high limit
                *result = tc_handler_get_u8_appdata_field(tc_handler, &mut high_limit);

                //get high limit evID
                *result = tc_handler_get_u16_appdata_field(tc_handler, &mut (limits_def.high_limit_evID));

                limits_def.low_limit = low_limit as u32;
                limits_def.high_limit = high_limit as u32;

            }
            case u32_t => {

                //get low limit
                *result = tc_handler_get_u32_appdata_field(tc_handler, &mut (limits_def.low_limit));
                
                //get low limit evID
                *result = tc_handler_get_u16_appdata_field(tc_handler, &mut (limits_def.low_limit_evID));

                //get high limit
                *result = tc_handler_get_u32_appdata_field(tc_handler, &mut (limits_def.low_limit));

                //get high limit evID
                *result = tc_handler_get_u16_appdata_field(tc_handler, &mut (limits_def.high_limit_evID));

            }
            case NoValidPID => {
                *result = Result::Error;
            }


        }

        var param_limit_check_definition : MonitorDefinition = MonitorDefinition::ParamLimitCheck(limits_def);
        
        return param_limit_check_definition;

    }


    viewer get_PMON_value_check_definition (&self, tc_handler : &mut TCHandlerT, result : &mut Result) -> MonitorDefinition {

        var value_def : ParamValueCheckDefinition = {
            mask_value = 0,
            expected_value = 0,
            EvID = 0
        };

        var type : DataPoolItemType = sys_data_pool_get_item_type(self->exect_tc_req_status_update.tc_data_5.mon_config.PID);

        match type {

            case u8_t => {

                var mask : u8 = 0;
                var expected_value : u8 = 0;

                //get mask
                *result = tc_handler_get_u8_appdata_field(tc_handler, &mut mask);

                //get expected value
                *result = tc_handler_get_u8_appdata_field(tc_handler, &mut expected_value);

                //get EvID
                *result = tc_handler_get_u16_appdata_field(tc_handler, &mut (value_def.EvID));

                value_def.mask_value = mask as u32;
                value_def.expected_value = expected_value as u32;

            }
            case u32_t => {

                //get mask
                *result = tc_handler_get_u32_appdata_field(tc_handler, &mut (value_def.mask_value));

                //get expected value
                *result = tc_handler_get_u32_appdata_field(tc_handler, &mut (value_def.expected_value));

                //get EvID
                *result = tc_handler_get_u16_appdata_field(tc_handler, &mut (value_def.EvID));

            }
            case NoValidPID => {
                *result = Result::Error;
            }

        }

        var expected_value_check_definition : MonitorDefinition = MonitorDefinition::ParamValueCheck(value_def);
     
        
        return expected_value_check_definition;

    }

    viewer get_TC_params(&self, tc_handler : &mut TCHandlerT, subtype : &mut u8, result : &mut Result) -> PS12ExecTCReqStatusUpdate {

        var tc_data : PS12ExecTCReqStatusUpdate = PS12ExecTCReqStatusUpdate_init();
        tc_data.packet_id = tc_handler->packet_header.packet_id;
        tc_data.packet_error_ctrl = tc_handler->packet_error_ctrl;
        tc_data.tc_num_bytes = tc_handler->tc_descriptor.tc_num_bytes;

        *subtype = tc_handler->df_header.subtype;

        if (*subtype == 1 || *subtype == 2 || *subtype == 6){

            var tc_data_1_2_6_aux : PS12TC_1_2_6_Data = {
                N = 0,
                PMONID = 0
            };

           
            *result = tc_handler_get_u8_appdata_field(tc_handler, &mut (tc_data_1_2_6_aux.N));
            *result = tc_handler_get_u16_appdata_field(tc_handler, &mut (tc_data_1_2_6_aux.PMONID));

            tc_data.tc_data_1_2_6 = tc_data_1_2_6_aux;

        } else if (*subtype == 5){

            var mon_config_aux : ParamMonitoringConfiguration = {
                    PID = 0,
                    type = MonitorCheckType::Free,
                    enabled = false,
                    definition = MonitorDefinition::Unselected,
                    repetition = 0,
                    repetition_control = 0,
                    interval = 0,
                    interval_control = 0,
                    transition_obt = {tv_sec = 0, tv_usec = 0},
                    temp_state = CheckState::Unselected,
                    current_state = CheckState::Unselected
            };
            var aux : u8 = 0;

            var tc_data_5_aux : PS12TC_5_Data = {
                N = 0,
                PMONID = 0,
                mon_config = mon_config_aux
            };

            *result = tc_handler_get_u8_appdata_field(tc_handler, &mut (tc_data_5_aux.N));
            *result = tc_handler_get_u16_appdata_field(tc_handler, &mut (tc_data_5_aux.PMONID));
            *result = tc_handler_get_u16_appdata_field(tc_handler, &mut tc_data_5_aux.mon_config.PID);
            *result = tc_handler_get_u8_appdata_field(tc_handler, &mut tc_data_5_aux.mon_config.interval);
            *result = tc_handler_get_u8_appdata_field(tc_handler, &mut tc_data_5_aux.mon_config.repetition);
            *result = tc_handler_get_u8_appdata_field(tc_handler, &mut aux);
            tc_data_5_aux.mon_config.type = get_check_type(aux);

            if (self->exect_tc_req_status_update.tc_data_5.mon_config.type is MonitorCheckType::ExpectedValue) {

                tc_data_5_aux.mon_config.definition = self->get_PMON_value_check_definition(tc_handler, result);

            }
            else if (self->exect_tc_req_status_update.tc_data_5.mon_config.type is MonitorCheckType::Limits){

                tc_data_5_aux.mon_config.definition = self->get_PMON_limit_check_definition(tc_handler, result);
            }
            else {
                //Error
            }

            tc_data.tc_data_5 = tc_data_5_aux;

        } else {

            // Invalid subtype

        }

        return tc_data;

    }

    procedure exec_tc (&mut self, tc_handler : &mut TCHandlerT, result: &mut Result){

        var subtype : u8 = 0;

        for i : usize in 0 .. 3 while (self->exect_tc_req_status is PS12ExecTCReqStatus::Exit == false) {

            match self->exect_tc_req_status {

                case Init => {

                    self->exect_tc_req_status_update = self->get_TC_params(tc_handler, &mut subtype, result);
                    if (*result is Result::Error) {
                        self->exect_tc_req_status = self->manage_short_pack_length_error();
                    } else {
                        self->exect_tc_req_status = PS12ExecTCReqStatus::ExecTC;
                    }

                }
                case ExecTC => {

                    if (subtype == 1) {

                        self->exect_tc_req_status = self->exec12_1TC();

                    } else if (subtype == 2) {

                        self->exect_tc_req_status = self->exec12_2TC();

                    } else if (subtype == 5) {

                        self->exect_tc_req_status = self->exec12_5TC();

                    } else if (subtype == 6) {

                        self->exect_tc_req_status = self->exec12_6TC();

                    } else {

                        self->exect_tc_req_status = PS12ExecTCReqStatus::Error;
                    }
                }

                case Error => {

                    *result = Result::Error;
                    self->exect_tc_req_status = PS12ExecTCReqStatus::Exit;

                }
                case Exit => {
                    //Unreachable point
                }  
            }
        }

        if(self->exect_tc_req_status is PS12ExecTCReqStatus::Exit){

            self->exect_tc_req_status = PS12ExecTCReqStatus::Init;
        }

        return;
    
    }

     
};




