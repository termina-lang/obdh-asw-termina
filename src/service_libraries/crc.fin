import service_libraries.tc_ccsds_pus_format;

/**
 * @brief Computes the CRC-16 checksum of a byte array
 * 
 * This function implements the CRC-16-CCITT algorithm with the polynomial 0x1021 
 * and initial value 0xFFFF. It is commonly used in CCSDS packet error detection.
 * 
 * @param[in] data     Reference to the array containing the bytes to be processed
 * @param[in] nbytes   Number of bytes to process from the data array
 * 
 * @return The computed 16-bit CRC value
 */
function cal_crc_16 (data : & [u8; max_tc_size], nbytes : usize) -> u16  {
    
    var crc_value : u16 = 0xFFFF : u16;
    var k : usize = 0;

    for i : usize in 0 : usize .. max_tc_size while k < nbytes {

        crc_value = crc_value ^ ((*data)[i] as u16 << 8 : u16);

        for j : u16 in 0 : u16 .. 8 : u16 {

            if ((crc_value & 0x8000 : u16) != 0 : u16) {
                crc_value = (crc_value << 1 : u16) ^ 0x1021 : u16;
            } else {
                crc_value = crc_value << 1 : u16;
            }
        }

        k = k + 1;
    }

	return crc_value;
}


