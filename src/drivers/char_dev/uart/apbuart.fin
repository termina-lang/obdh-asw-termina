import drivers.char_dev;

import service_libraries.queue_u8;
import service_libraries.errors;

/**
 * @file uart.fin
 * @brief UART driver implementation using memory-mapped register access.
 */

const hw_fifo_size : usize = 1;

constexpr uart_dr : u32 = 0x01;
constexpr uart_te : u32 = 0x04;



/**
 * @struct APBUARTRegs
 * @brief Memory-mapped UART register block.
 *
 * This structure represents the UART hardware registers.
 */
//#[packed]
struct APBUARTRegs {
    /** Data register (read/write). */
    data : u32;
    /** Status register (read-only). */
    status : u32;
    /** Control register (read/write). */
    control :  u32;
    /** Clock scaler register (read/write). */
    scaler : u32;
};


/**
 * @interface CharDevInit
 * @brief Interface for initializing a character device.
 */
interface IAPBUARTInit {
    procedure initialize(&mut self);
};


/**
 * @class UARTDriver
 * @brief UART driver implementing character device interfaces.
 *
 * This class handles UART transmission and reception using internal
 * state machines, software queues, and hardware memory-mapped registers.
 */
resource class CAPBUARTDriver provides 
        ICharDevSend, IAPBUARTInit, ICharDevNotifyIrq {
    
    // UART registers
    registers : loc APBUARTRegs;

    //Flag to indicate that there are bytes enqueued that must be sent
    rem_bytes : bool;

    // TX queue
    uart_tx_queue : QueueU8;

    rx_queue : access ICharDevEnqueueRx;

    /**
     * @brief Checks if transmit FIFO is full.
     * @return True if full, false otherwise.
     */
    method tf_is_full(&self) -> bool {
        let riscv_uart_tf : u32 = 0x00000200 : u32;
        return ((self->registers.status & riscv_uart_tf) != 0 : u32);
    }


    /**
     * @brief Checks if transmit FIFO is empty.
     * @return True if empty, false otherwise.
     */
    method tf_is_empty(&self) -> bool {
        let riscv_uart_te : u32 = 0x00000004 : u32;
        return ((self->registers.status & riscv_uart_te) != 0 : u32);
    }


    /**
     * @brief Checks if receive FIFO has data available.
     * @return True if data is ready, false otherwise.
     */
    method rf_data_ready(&self) -> bool {
        let riscv_uart_rf : u32 = 0x00000001 : u32;
        return ((self->registers.status & riscv_uart_rf) != 0 : u32);
    }


    /**
     * @brief Enables UART transmission.
     */
    method enable_TX(&priv self) {
        let riscv_uart_txe : u32 = 0x00000002 : u32;
        self->registers.control = self->registers.control | riscv_uart_txe;
        return;
    }


    /**
     * @brief Enables UART reception.
     */
    method enable_RX(&priv self) {
        let riscv_uart_rxe : u32 = 0x00000001 : u32;
        self->registers.control = self->registers.control | riscv_uart_rxe;
        return;
    }


    /**
     * @brief Enables reception interrupts.
     */
    method enable_RI(&priv self) {
        let riscv_uart_ri : u32 = 0x00000004 : u32;
        self->registers.control = self->registers.control | riscv_uart_ri;
        return;
    }


    /**
     * @brief Enables transmission interrupts.
     */
    method enable_TI(&priv self) {
        let riscv_uart_ti : u32 = 0x00000008 : u32;
        self->registers.control = self->registers.control | riscv_uart_ti;
        return;
    }


    /**
     * @brief Disables transmission interrupts.
     */
    method disable_TI(&priv self) {
        let riscv_uart_ti : u32 = 0xFFFFFFF7 : u32;
        self->registers.control = self->registers.control & riscv_uart_ti;
        return;
    }


    /**
     * @brief Disables transmission FIFO.
     */
    method disable_TF(&priv self) {
        let riscv_uart_tf : u32 = 0xFFFFFCFF : u32;
        self->registers.control = self->registers.control & riscv_uart_tf;
        return;
    }


    /**
     * @brief Disables reception FIFO.
     */
    method disable_RF(&priv self) {
        let riscv_uart_rf : u32 = 0xFFFFFBFF : u32;
        self->registers.control = self->registers.control & riscv_uart_rf;
        return;
    }


    procedure notify_irq(&mut self, result : &mut Result<bool; i32>) {

        let uart_status : u32 = self->registers.status;
        *result = Ok(false);
       
        if ((uart_status & uart_te) != 0 : u32) {

            self->release_tx();

        }

        if ((uart_status & uart_dr) != 0 : u32) {

            var byte : u8 = self->registers.data as u8;
            self->rx_queue.enqueue_rx(byte, result);

        }

        return;

    }
    

    /**
     * @brief Writes a number of bytes equal to the HW FIFO size to UART if pending, from the TX queue.
     *
     * This is called from the interrupt handler.
     */
    method release_tx(&mut self) {

        if self->rem_bytes {

            var num_elements : usize = 0;

            get_num_enqueued_elems (&(self->uart_tx_queue), &mut num_elements);

            var sent_bytes : usize = 0;
            var extracted_elem : Option<u8> = None;

            for i : usize in 0 : usize .. hw_fifo_size {

                dequeue(&mut(self->uart_tx_queue), &mut extracted_elem);

                match extracted_elem {
                    case Some(elem) => {
                        self->registers.data = elem as u32;
                        sent_bytes = sent_bytes + 1;
                    }
                    case None => {
                        //queue is empty
                    }
                }

            }

            if (sent_bytes < num_elements) {
                
                self->rem_bytes = true;

            } else {

                self->rem_bytes = false;
                
            }
            

        }

        return;
    }



    /**
     * @brief Sends a buffer through UART.
     *
     * If possible, sends immediately a number of bytes equal to the HW FIFO size; otherwise enqueues.
     *
     * @param[in] nbytes Number of bytes to send.
     * @param[in] output_bytes Bytes to be transmitted.
     * @param[out] status Success or failure (e.g. buffer full).
     */
    procedure send(&mut self, nbytes : const usize,
                   output_bytes : & [u8; nbytes], 
                   status : &mut Status<i32>) {

        *status = Success;

        if (self->rem_bytes) {

            var num_elements : usize = 0;
            get_num_enqueued_elems(&(self->uart_tx_queue), &mut num_elements);

            if queue_max_noe - num_elements >= nbytes {

                for i : usize in 0 : usize .. nbytes while (*status is Failure) == false {

                    *status = enqueue(&mut(self->uart_tx_queue), output_bytes[i]);

                }

            } else {
                *status = Failure(TM_SEND_FAILURE);
            }

        } else {

            var sent_bytes : usize = 0;

            if self->tf_is_empty() {

                for i : usize in 0 .. hw_fifo_size while i < nbytes {

                    self->registers.data = output_bytes[i] as u32;
            
                    sent_bytes = sent_bytes + 1;
                    
                }

            }

            if sent_bytes < nbytes {

                self->rem_bytes = true;

                var left_bytes : usize = nbytes - sent_bytes;
                var num_elements : usize = 0;

                get_num_enqueued_elems(&(self->uart_tx_queue), &mut num_elements);

                if queue_max_noe - num_elements >= left_bytes {

                    for i : usize in 0 .. nbytes while (i < left_bytes && (*status is Failure) == false){

                        *status = enqueue(&mut(self->uart_tx_queue), output_bytes[i + sent_bytes]);

                    }


                } else {
                    
                    *status = Failure(TM_SEND_FAILURE);
                     
                }

            }

        }

        return;

    }



    /**
     * @brief Initializes the UART module.
     *
     * Enables TX, RX, and interrupts.
     */
    procedure initialize(&mut self) {

        self->enable_RX();
        self->enable_TX();
        self->enable_RI();
        self->enable_TI();
        self->disable_TF();
        self->disable_RF();

        return;

    }

};


